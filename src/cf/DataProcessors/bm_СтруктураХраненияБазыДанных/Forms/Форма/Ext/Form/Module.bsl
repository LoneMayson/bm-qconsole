
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТЗ = ПолучитьСтруктуруХраненияБазыДанных();
	ЭтотОбъект.Таблицы.Загрузить(ТЗ);
	ЭтотОбъект.Таблицы.Сортировать("Метаданные Возр, ИмяТаблицы Убыв, Назначение Возр");
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если Не ПустаяСтрока(ТекМетаданные) Тогда
		ЗаполнитьТаблицу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Таблицы.ТекущиеДанные;
	Если ПустаяСтрока(ТекДанные.Метаданные) Тогда
		Возврат;
	КонецЕсли;
	
	ТекИмяТаблицы = ТекДанные.ИмяТаблицы;
	ТекИмяТаблицыХранения = ТекДанные.ИмяТаблицыХранения;
	ТекМетаданные = ТекДанные.Метаданные; 
	
	ЗаполнитьТаблицу();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПоляТаблицы;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзбранноеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Избранное.ТекущиеДанные;
	Если ПустаяСтрока(ТекДанные.Метаданные) Тогда
		Возврат;
	КонецЕсли;
	
	ТекИмяТаблицы = ТекДанные.ИмяТаблицы;
	ТекИмяТаблицыХранения = ТекДанные.ИмяТаблицыХранения;
	ТекМетаданные = ТекДанные.Метаданные; 
	
	ЗаполнитьТаблицу();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПоляТаблицы;
	
КонецПроцедуры

&НаКлиенте
Процедура ИндексыПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элементы.Индексы.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ПоляИндекса.ОтборСтрок = Новый ФиксированнаяСтруктура("КлючСвязи", ТекДанные.КлючСвязи);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВИзбранное(Команда)
	
	ТекДанные = Элементы.Таблицы.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ИмяТаблицы, ИмяТаблицыХранения");
	ЗаполнитьЗначенияСвойств(Отбор, ТекДанные);
	МС = ЭтотОбъект.Избранное.НайтиСтроки(Отбор);
	Если МС.Количество() = 0 Тогда
		НовСтр = ЭтотОбъект.Избранное.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, ТекДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицу()
	
	Массив = Новый Массив();
	Массив.Добавить(ТекМетаданные);
	
	ТЗ = ПолучитьСтруктуруХраненияБазыДанных(Массив);
	Отбор = Новый Структура("ИмяТаблицы, ИмяТаблицыХранения, Метаданные", ТекИмяТаблицы, ТекИмяТаблицыХранения, ТекМетаданные); 
	МС = ТЗ.НайтиСтроки(Отбор);
	Если МС.Количество() > 0 Тогда
		ИскомаяСтрока = МС[0];
		ЭтотОбъект.ПоляТаблицы.Загрузить(ИскомаяСтрока.Поля);
		ЗаполнитьИндексы(ИскомаяСтрока.Индексы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИндексы(ТаблицаИндексы)
	
	ЭтотОбъект.Индексы.Очистить();
	ЭтотОбъект.ПоляИндекса.Очистить();
	
	КлючСвязи = 1;
	Для Каждого СтрокаИндекса Из ТаблицаИндексы Цикл 
		НовСтр = ЭтотОбъект.Индексы.Добавить();
		НовСтр.ИмяИндексаХранения = СтрокаИндекса.ИмяИндексаХранения;
		НовСтр.КлючСвязи = КлючСвязи;
		Для Каждого ПолеИндекса Из СтрокаИндекса.Поля Цикл
			НовСтр = ЭтотОбъект.ПоляИндекса.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ПолеИндекса);
			НовСтр.КлючСвязи = КлючСвязи;
		КонецЦикла;
		КлючСвязи = КлючСвязи + 1; 
	КонецЦикла;
	
КонецПроцедуры






