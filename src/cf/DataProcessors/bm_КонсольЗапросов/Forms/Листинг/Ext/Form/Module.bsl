
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстДок = ЛистингИзТекста(Параметры.Текст);
	ТекстДок = ДобавитьТабуляцию(ТекстДок);
	
	ЭтотОбъект.Листинг.УстановитьТекст(ТекстДок.ПолучитьТекст());
	
КонецПроцедуры

&НаСервере
Функция ЛистингИзТекста(ТекстЗапроса)	
	
	ТекстДок = Новый ТекстовыйДокумент();
	
	ЧислоСтрок = СтрЧислоСтрок(ТекстЗапроса);
	Для Итер = 1 По ЧислоСтрок Цикл
		СтрокаЗапроса = СтрПолучитьСтроку(ТекстЗапроса, Итер);
		СтрокаЗапроса = СтрЗаменить(СтрокаЗапроса, """", """""");
		Префикс 	= ?(Итер = 1, """", "|");
		Постфикс 	= ?(Итер = ЧислоСтрок, """;", ""); 
		ТекстДок.ДобавитьСтроку(Префикс + СтрокаЗапроса + Постфикс);
	КонецЦикла;
	
	Возврат ТекстДок;
	
КонецФункции

&НаСервере
Функция ДобавитьТабуляцию(Источник)
	
	Приемник = Новый ТекстовыйДокумент();
	
	Для Итер = 1 По Источник.КоличествоСтрок() Цикл
		Приемник.ДобавитьСтроку(Символы.Таб + Источник.ПолучитьСтроку(Итер));
	КонецЦикла;
	
	Возврат Приемник;
	
КонецФункции