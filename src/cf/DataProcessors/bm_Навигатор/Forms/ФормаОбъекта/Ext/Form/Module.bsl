#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.Описание = Параметры.Описание;
	ЭтотОбъект.Настройки = Параметры.Настройки;
	ЭтотОбъект.Ссылка = Параметры.Ссылка;
	
	Если ЭтотОбъект.Описание.ЭтоРегистрСведений Тогда
		ВыражениеТипа = "РегистрСведенийМенеджерЗаписи." + ЭтотОбъект.Описание.Имя;
	ИначеЕсли ЭтотОбъект.Описание.ЭтоВнешИсточник Тогда
		Если ЭтотОбъект.Описание.ТипДанныхТаблицы = "ОбъектныеДанные" Тогда
			ВыражениеТипа = ЭтотОбъект.Описание.ВыражениеТипаОбъект;
		// не реализовано, ошибка создания реквизита
		Иначе
			ВыражениеТипа = ЭтотОбъект.Описание.ВыражениеТипаОбъект;
		КонецЕсли;
	Иначе
		ВыражениеТипа = ЭтотОбъект.Описание.ИмяЕдч + "Объект." + ЭтотОбъект.Описание.Имя;
	КонецЕсли;
	
	ДобавляемыеРеквизиты = Новый Массив();
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОбъектФормы", Новый ОписаниеТипов(ВыражениеТипа)));
	ЭтотОбъект.ИзменитьРеквизиты(ДобавляемыеРеквизиты); 
	
    НастроитьФорму();
		
	ЭтотОбъект.АвтоЗаголовок = Ложь;
	ЭтотОбъект.Заголовок = ВыражениеТипа;
		
	Если ЭтотОбъект.Описание.ТЧ.Имена.Количество() > 0 Тогда
		СоздатьЭлементыТЧ();
	КонецЕсли;
	
	Если Параметры.Свойство("СсылкаКопирования") Тогда
		ПрочитатьСсылкуКопирования(Параметры.СсылкаКопирования);
	Иначе
		ПрочитатьОбъект();
	КонецЕсли;
	
	УпрФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЭтотОбъект.Модифицированность Тогда
		Отказ = Истина;
		Если Не ЗавершениеРаботы Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ВопросОСохранении_Завершение", ЭтотОбъект, Новый Структура()), "Данные были изменены. Остаться в форме?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура РеквизитыОбъектаПриАктивизацииЯчейки(Элемент)
	
	ТекДанные = Элементы.РеквизитыОбъекта.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элемент.ТекущийЭлемент.ОграничениеТипа = ТекДанные.Тип;	
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОбъектаПометкаПриИзменении(Элемент)
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыОбъектаЗначениеПриИзменении(Элемент)
	
	ТекДанные = Элементы.РеквизитыОбъекта.ТекущиеДанные;
	ТекДанные.Пометка = Истина;
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыСтрокиПриАктивизацииЯчейки(Элемент)

	ТекДанные = Элементы.РеквизитыСтроки.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элемент.ТекущийЭлемент.ОграничениеТипа = ТекДанные.Тип;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыСтрокиЗначениеПриИзменении(Элемент)

	ТекДанные = Элементы.РеквизитыСтроки.ТекущиеДанные;
	ИмяПоля = ТекДанные.Имя;
	Значение = ТекДанные.Значение;
	
	ТаблицаФормы = Элементы["ТаблицаТЧ_" + ЭтотОбъект.ТекИмяТЧ];
	ТекДанные = ТаблицаФормы.ТекущиеДанные;	
	ТекДанные[ИмяПоля] = Значение;
	
	ИскомаяСтрока = ЭтотОбъект.ТабличныеЧасти.НайтиСтроки(Новый Структура("Имя", ЭтотОбъект.ТекИмяТЧ))[0];
	ИскомаяСтрока.Пометка = Истина;
	
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТабличныеЧастиПриАктивизацииСтроки(Элемент)
	
	ТекДанные = Элементы.ТабличныеЧасти.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницыТабличныеЧасти.ТекущаяСтраница = Элементы["ГруппаТЧ_" + ТекДанные.Имя];
	
	ЭтотОбъект.ТекИмяТЧ = ТекДанные.Имя;
	
	Если ЭтотОбъект.Настройки.НастройкаВыводитьРеквизитыСтроки Тогда
		ЗаполнитьРеквизитыСтроки("ТабличныеЧасти");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФормыПриАктивизацииСтроки(Элемент)
	
	Если ЭтотОбъект.Настройки.НастройкаВыводитьРеквизитыСтроки Тогда
		ЗаполнитьРеквизитыСтроки("ТаблицаФормы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФормыПриИзменении(Элемент)
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФормыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ОбновитьКоличествоСтрокТЧ(ЭтотОбъект, ЭтотОбъект.ТекИмяТЧ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФормыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Не ОтменаРедактирования Тогда
		
		ИскомаяСтрока = ЭтотОбъект.ТабличныеЧасти.НайтиСтроки(Новый Структура("Имя", ЭтотОбъект.ТекИмяТЧ))[0];
		ИскомаяСтрока.Пометка = Истина;	
		ЭтотОбъект.Модифицированность = Истина;
	
		Если ЭтотОбъект.Настройки.НастройкаВыводитьРеквизитыСтроки Тогда
			ЗаполнитьРеквизитыСтроки("ТаблицаФормы");
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаФормыПослеУдаления(Элемент)
	
	ИскомаяСтрока = ЭтотОбъект.ТабличныеЧасти.НайтиСтроки(Новый Структура("Имя", ЭтотОбъект.ТекИмяТЧ))[0];
	ИскомаяСтрока.Пометка = Истина;	
	ЭтотОбъект.Модифицированность = Истина;
		
	ОбновитьКоличествоСтрокТЧ(ЭтотОбъект, ЭтотОбъект.ТекИмяТЧ);
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура КомандаПрочитать(Команда)
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьОбъект();
	
	ЭтотОбъект.Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	ЗаписатьПровестиКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПровести(Команда)
	
	ЗаписатьПровестиКлиент(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтменаПроведения(Команда)
	
	Если Не ЭтотОбъект["ОбъектФормы"].Проведен Тогда
		Возврат;
	КонецЕсли;
	
	ОтменаПроведения();
	
	ЭтотОбъект.Модифицированность = Ложь;
	
	ВыражениеТипа = ЭтотОбъект.Описание.ИмяЕдч + "Ссылка." + ЭтотОбъект.Описание.Имя;
	ОповеститьОбИзменении(Тип(ВыражениеТипа));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбработкаДвиженияДокумента(Команда)
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		ПоказатьПредупреждение(, "Объект не записан");
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Описание,Настройки,Ссылка");
	ЗаполнитьЗначенияСвойств(ПараметрыОткрытия, ЭтотОбъект);
	ОткрытьФорму(ИмяФормыОбработки("ФормаОбработкаДвиженияДокумента"), ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтчетДвиженияДокумента(Команда)

	Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		ПоказатьПредупреждение(, "Объект не записан");
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Документ", ЭтотОбъект.Ссылка);
	ОткрытьФорму(ИмяФормыОбработки("ФормаОтчетДвиженияДокумента"), ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьЗначение(Команда)
	
	ВыборЗначения_ВыбратьЗначение();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьЗначение(Команда)

	ВыборЗначения_ОткрытьЗначение();
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаРеквизитыСтрокиВыбратьЗначение(Команда)

	ВыборЗначения_ВыбратьЗначение("РеквизитыСтроки");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаРеквизитыСтрокиОткрытьЗначение(Команда)
	
	ВыборЗначения_ОткрытьЗначение("РеквизитыСтроки");
		
КонецПроцедуры

&НаКлиенте
Процедура КомандаРедактироватьОбъект(Команда)
	
	Элементы.ФормаКомандаРедактироватьОбъект.Пометка = Не Элементы.ФормаКомандаРедактироватьОбъект.Пометка;
	УпрФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ЧтениеЗаписьОбъекта

&НаСервере
Процедура ПрочитатьСсылкуКопирования(СсылкаКопирования)
	
	Если ЭтотОбъект.Описание.ЭтоРегистрСведений Тогда
    	ОбъектИнфобазыИсточник = РегистрыСведений[ЭтотОбъект.Описание.Имя].СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ОбъектИнфобазыИсточник, СсылкаКопирования);
		ОбъектИнфобазыИсточник.Прочитать();
		ОбъектИнфобазы = РегистрыСведений[ЭтотОбъект.Описание.Имя].СоздатьМенеджерЗаписи();
		ЗначениеВРеквизитФормы(ОбъектИнфобазы, "ОбъектФормы");
		//непонятно как ещё заполнить, минуя исходный ключ записи
		ЗаполнитьЗначенияСвойств(ЭтотОбъект["ОбъектФормы"], ОбъектИнфобазыИсточник);
		ОбъектИнфобазы = РеквизитФормыВЗначение("ОбъектФормы");
	ИначеЕсли ЭтотОбъект.Описание.ЭтоВнешИсточник Тогда
		ОбъектИнфобазы = СсылкаКопирования.Скопировать();
		ЗначениеВРеквизитФормы(ОбъектИнфобазы, "ОбъектФормы");
	Иначе
		ОбъектИнфобазы = СсылкаКопирования.Скопировать();
		ЗначениеВРеквизитФормы(ОбъектИнфобазы, "ОбъектФормы");
	КонецЕсли;
	
	ЗаполнитьРеквизитыОбъекта(ОбъектИнфобазы);
	
	ОбновитьКоличествоСтрокТЧ(ЭтотОбъект);
	ЗаполнитьСтатус();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьОбъект()
	
	Если ЭтотОбъект.Описание.ЭтоРегистрСведений Тогда
    	ОбъектИнфобазы = РегистрыСведений[ЭтотОбъект.Описание.Имя].СоздатьМенеджерЗаписи();
		Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		Иначе
			ЗаполнитьЗначенияСвойств(ОбъектИнфобазы, ЭтотОбъект.Ссылка);
			ОбъектИнфобазы.Прочитать();
			КлючЗаписи = РегистрыСведений[ЭтотОбъект.Описание.Имя].СоздатьКлючЗаписи(ЭтотОбъект.Ссылка);
			ЭтотОбъект.RefURL = ПолучитьНавигационнуюСсылку(КлючЗаписи);
		КонецЕсли;
		ЗначениеВРеквизитФормы(ОбъектИнфобазы, "ОбъектФормы");
	ИначеЕсли ЭтотОбъект.Описание.ЭтоВнешИсточник Тогда
		Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
			ОбъектИнфобазы = Новый(ЭтотОбъект.Описание.ВыражениеТипаОбъект);
		Иначе
			ОбъектИнфобазы = ЭтотОбъект.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		ЭтотОбъект.Ссылка = ОбъектИнфобазы.Ссылка;
		ЭтотОбъект.RefURL = ПолучитьНавигационнуюСсылку(ЭтотОбъект.Ссылка);
		ЗначениеВРеквизитФормы(ОбъектИнфобазы, "ОбъектФормы");
	Иначе                                       
		Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
			ОбъектИнфобазы = Новый(ЭтотОбъект.Описание.ИмяЕдч + "Объект." + ЭтотОбъект.Описание.Имя);
		Иначе
			ОбъектИнфобазы = ЭтотОбъект.Ссылка.ПолучитьОбъект();
		КонецЕсли;                                             
		ЭтотОбъект.Ссылка = ОбъектИнфобазы.Ссылка;
		ЭтотОбъект.RefUUID = ЭтотОбъект.Ссылка.УникальныйИдентификатор();
		ЭтотОбъект.RefURL = ПолучитьНавигационнуюСсылку(ЭтотОбъект.Ссылка);
		ЗначениеВРеквизитФормы(ОбъектИнфобазы, "ОбъектФормы");
	КонецЕсли;
	
	ЗаполнитьРеквизитыОбъекта(ОбъектИнфобазы);
	
	ОбновитьКоличествоСтрокТЧ(ЭтотОбъект);
	ЗаполнитьСтатус();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьОбъектИнфобазы(ОбъектИнфобазы)
	
	ЗначениеВРеквизитФормы(ОбъектИнфобазы, "ОбъектФормы");
	ЗаполнитьРеквизитыОбъекта(ОбъектИнфобазы);
	ОбновитьКоличествоСтрокТЧ(ЭтотОбъект);
	ЗаполнитьСтатус();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьПровестиКлиент(Знач Провести = Ложь)
	
	ЗаписатьПровести(Провести);
	
	ЭтотОбъект.Модифицированность = Ложь;     
	
	Если ЭтотОбъект.Описание.ЭтоРегистрСведений Тогда
		ВыражениеТипа = "РегистрСведенийКлючЗаписи." + ЭтотОбъект.Описание.Имя;
	ИначеЕсли ЭтотОбъект.Описание.ЭтоВнешИсточник Тогда
		ВыражениеТипа = ЭтотОбъект.Описание.ВыражениеТипаСсылка;
	Иначе
		ВыражениеТипа = ЭтотОбъект.Описание.ИмяЕдч + "Ссылка." + ЭтотОбъект.Описание.Имя;
	КонецЕсли;	
	ОповеститьОбИзменении(Тип(ВыражениеТипа));
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПровести(Знач Провести = Ложь)
	
	УстановитьРеквизитыОбъекта();
	
	ОбъектИнфобазы = РеквизитФормыВЗначение("ОбъектФормы");
	
	Если Не (ЭтотОбъект.Описание.ЭтоРегистрСведений Или ЭтотОбъект.Описание.ЭтоВнешИсточник) И Не Провести Тогда
		ОбъектИнфобазы.ОбменДанными.Загрузка = ЭтотОбъект.ОбменДаннымиЗагрузка;
	КонецЕсли;
	
	Если Не ЭтотОбъект.Описание.ЭтоРегистрСведений И Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		Если СтрНайти("Справочник,ПланВидовХарактеристик", ЭтотОбъект.Описание.ИмяЕдч) > 0
			И ЭтотОбъект.Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Код")
			И Метаданные[ЭтотОбъект.Описание.ИмяМнч][ЭтотОбъект.Описание.Имя].Автонумерация Тогда
			// Планы обмена?
			ОбъектИнфобазы.УстановитьНовыйКод();
		ИначеЕсли СтрНайти("Документ,БизнесПроцесс,Задача", ЭтотОбъект.Описание.ИмяЕдч) > 0
			И ЭтотОбъект.Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Номер")
			И Метаданные[ЭтотОбъект.Описание.ИмяМнч][ЭтотОбъект.Описание.Имя].Автонумерация Тогда
			ОбъектИнфобазы.УстановитьНовыйНомер();
		КонецЕсли;                                                                               
		Если ЭтотОбъект.Описание.ИмяЕдч = "Документ" Тогда
			ОбъектИнфобазы.Дата = ТекущаяДата();
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтотОбъект.Описание.ЭтоРегистрСведений Тогда
		ОбъектИнфобазы.Записать(Ложь);
	ИначеЕсли Провести Тогда
		ОбъектИнфобазы.Записать(РежимЗаписиДокумента.Проведение);	
	Иначе
		ОбъектИнфобазы.Записать();
	КонецЕсли;
	
	Если ЭтотОбъект.Описание.ЭтоРегистрСведений Тогда
		ЭтотОбъект.Ссылка = СкопироватьСтруктуру(ЭтотОбъект.Описание.КлючЗаписи);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект.Ссылка, ОбъектИнфобазы);
		КлючЗаписи = РегистрыСведений[ЭтотОбъект.Описание.Имя].СоздатьКлючЗаписи(ЭтотОбъект.Ссылка);
		ЭтотОбъект.RefURL = ПолучитьНавигационнуюСсылку(КлючЗаписи);
	ИначеЕсли ЭтотОбъект.Описание.ЭтоВнешИсточник Тогда
		ЭтотОбъект.Ссылка = ОбъектИнфобазы.Ссылка;
		ЭтотОбъект.RefURL = ПолучитьНавигационнуюСсылку(ЭтотОбъект.Ссылка);
	Иначе
		ЭтотОбъект.Ссылка = ОбъектИнфобазы.Ссылка;
		ЭтотОбъект.RefUUID = ЭтотОбъект.Ссылка.УникальныйИдентификатор();
		ЭтотОбъект.RefURL = ПолучитьНавигационнуюСсылку(ЭтотОбъект.Ссылка);
	КонецЕсли;
	
	ПрочитатьОбъектИнфобазы(ОбъектИнфобазы);
	
КонецПроцедуры

&НаСервере
Процедура ОтменаПроведения()
	
	УстановитьРеквизитыОбъекта();
	
	ОбъектИнфобазы = РеквизитФормыВЗначение("ОбъектФормы");
	ОбъектИнфобазы.Записать(РежимЗаписиДокумента.ОтменаПроведения);

	ПрочитатьОбъектИнфобазы(ОбъектИнфобазы);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыОбъекта(ОбъектИнфобазы)
	
	ЭтотОбъект.РеквизитыОбъекта.Очистить();
	
	ПроверятьИспользование = (ЭтотОбъект.Описание.Свойство("Иерархический") И ЭтотОбъект.Описание.ВидИерархии = "ИерархияГруппИЭлементов");
	
	Если ЭтотОбъект.Описание.ЭтоВнешИсточник Тогда
		МетаданныеОбъекта = Вычислить("Метаданные." + Описание.ИмяМнч + "." + Описание.Имя);
	Иначе
		МетаданныеОбъекта = Метаданные[ЭтотОбъект.Описание.ИмяМнч][ЭтотОбъект.Описание.Имя];
	КонецЕсли;
	
	Для Каждого ИмяКоллекции Из ЭтотОбъект.Описание.Реквизиты.Имена Цикл
		ТекКоллекция = ЭтотОбъект.Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекКоллекция Цикл
			Если ЭлемОписание.Имя = "Ссылка" И ИмяКоллекции = "СтандартныеРеквизиты" Тогда
				Продолжить;
			КонецЕсли;
			Если ПроверятьИспользование И ИмяКоллекции = "Реквизиты" Тогда
				Использование = МетаданныеОбъекта.Реквизиты[ЭлемОписание.Имя].Использование;				
				Если (ОбъектИнфобазы.ЭтоГруппа И Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента)
					Или (Не ОбъектИнфобазы.ЭтоГруппа И Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			НовСтр = ЭтотОбъект.РеквизитыОбъекта.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, ЭлемОписание);
			НовСтр.Значение = ОбъектИнфобазы[ЭлемОписание.Имя];
			НовСтр.ТипЗначения = ТипЗнч(НовСтр.Значение);
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				НовСтр.ЭтоХранилищеЗначения = Истина;
				НовСтр.Значение = "<Хранилище значения>";
			КонецЕсли;                                      
			НовСтр.ИмяКоллекции = ИмяКоллекции;
			Если ИмяКоллекции = "Реквизиты" Или ИмяКоллекции = "СтандартныеРеквизиты" Тогда
				НовСтр.Картинка = БиблиотекаКартинок.Реквизит;
			ИначеЕсли СтрНачинаетсяС(ИмяКоллекции, "Измерения") Или СтрНачинаетсяС(ИмяКоллекции, "Счет") Тогда
				НовСтр.Картинка = БиблиотекаКартинок.Измерение;
			ИначеЕсли СтрНачинаетсяС(ИмяКоллекции, "Ресурсы") Тогда
				НовСтр.Картинка = БиблиотекаКартинок.Ресурс;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ЭтотОбъект.КоличествоРеквизитов = ЭтотОбъект.РеквизитыОбъекта.Количество();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатус()
	
	Если ЭтотОбъект.Описание.ЭтоРегистрСведений Или ЭтотОбъект.Описание.ЭтоВнешИсточник Тогда
		Элементы.ДекорацияСтатус.Видимость = Ложь;
		Возврат;
	КонецЕсли;

	Если ЭтотОбъект.Описание.ИмяЕдч = "Документ" Тогда
		Если ЭтотОбъект["ОбъектФормы"].Проведен Тогда
			Элементы.ДекорацияСтатус.Картинка = Элементы.ДекорацияПроведен.Картинка;
		ИначеЕсли ЭтотОбъект["ОбъектФормы"].ПометкаУдаления Тогда
			Элементы.ДекорацияСтатус.Картинка = Элементы.ДекорацияПометкаУдаления.Картинка;
		Иначе
			Элементы.ДекорацияСтатус.Картинка = Элементы.ДекорацияДокумент.Картинка;
		КонецЕсли;
		Элементы.ФормаКомандаОтменаПроведения.Доступность = (Элементы.ФормаКомандаРедактироватьОбъект.Пометка И ЭтотОбъект["ОбъектФормы"].Проведен);
	Иначе
		Если ЭтотОбъект["ОбъектФормы"].ПометкаУдаления Тогда
			Элементы.ДекорацияСтатус.Картинка = Элементы.ДекорацияПометкаУдаления.Картинка;
		Иначе
			Элементы.ДекорацияСтатус.Картинка = Элементы.ДекорацияДокумент.Картинка;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыОбъекта()
	
	Для Каждого СтрокаТЗ Из ЭтотОбъект.РеквизитыОбъекта Цикл
		Если СтрокаТЗ.ЭтоХранилищеЗначения Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаТЗ.Пометка Тогда
			ЭтотОбъект["ОбъектФормы"][СтрокаТЗ.Имя] = СтрокаТЗ.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыСтроки(Источник)
	
	ЭтотОбъект.РеквизитыСтроки.Очистить();
	
	ТаблицаФормы = Элементы["ТаблицаТЧ_" + ЭтотОбъект.ТекИмяТЧ];
	ТекДанные = ТаблицаФормы.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлемОписание Из ЭтотОбъект.Описание.ТЧ.ГруппыРеквизитовМассив[ЭтотОбъект.ТекИмяТЧ] Цикл
		НовСтр = ЭтотОбъект.РеквизитыСтроки.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, ЭлемОписание);
		Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
			НовСтр.Значение = "<Хранилище значения>";
			НовСтр.ТипЗначения = "Хранилище значения";
		Иначе
			НовСтр.Значение = ТекДанные[ЭлемОписание.Имя];
			НовСтр.ТипЗначения = ТипЗнч(НовСтр.Значение);
		КонецЕсли;
		НовСтр.Картинка = БиблиотекаКартинок.Реквизит;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ВыбратьОткрытьЗначение

&НаКлиенте
Процедура ВыборЗначения_ВыбратьЗначение(Источник = "")
	
	ДополнительныеПараметры = ВыборЗначения_ПараметрыРедактирования(Источник);
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивТипов = ВыборЗначения_МассивТиповФильтр(ДополнительныеПараметры.ОписаниеТипов.Типы());
	Если МассивТипов.Количество() = 0 Тогда
		Возврат;
	ИначеЕсли МассивТипов.Количество() = 1 Тогда
		ВыборЗначения_ВыборТипаЗначенияЗавершение(Новый Структура("Значение", МассивТипов[0]), ДополнительныеПараметры);
	Иначе
		СписокЗначений = Новый СписокЗначений();
		СписокЗначений.ЗагрузитьЗначения(МассивТипов);
		Оповещение = Новый ОписаниеОповещения("ВыборЗначения_ВыборТипаЗначенияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		СписокЗначений.ПоказатьВыборЭлемента(Оповещение, "Выберите тип");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗначения_ВыборТипаЗначенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Тип = Результат.Значение;
	
	Если Тип = Тип("УникальныйИдентификатор") Тогда
		ПараметрыОткрытия = Новый Структура("УИД", ДополнительныеПараметры.Значение);
		Оповещение = Новый ОписаниеОповещения("ВыборЗначения_ВыбратьЗначениеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму(ИмяФормыОбработки("КонструкторУИД"), ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор,,, Оповещение);
	ИначеЕсли Тип = Тип("Строка") Тогда                                       
		Оповещение = Новый ОписаниеОповещения("ВыборЗначения_ВыбратьЗначениеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВводСтроки(Оповещение, ДополнительныеПараметры.Значение,,, ДополнительныеПараметры.МногострочныйРежим);
	Иначе
		ПараметрыОткрытия = Новый Структура("Описание,Настройки,РежимВыбора");
		ПараметрыОткрытия.Описание = ОписаниеОбъектаМетаданных(Тип);
		ПараметрыОткрытия.Настройки = ЭтотОбъект.Настройки;
		ПараметрыОткрытия.РежимВыбора = Истина;
		Оповещение = Новый ОписаниеОповещения("ВыборЗначения_ВыбратьЗначениеЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
		ОткрытьФорму(ИмяФормыОбработки("ФормаСписка"), ПараметрыОткрытия, ЭтотОбъект, "РежимВыбора" + ПараметрыОткрытия.Описание.ОсновнаяТаблица,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗначения_ВыбратьЗначениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Источник = "РеквизитыСтроки" Тогда
		ТекДанные = Элементы.РеквизитыСтроки.ТекущиеДанные;
		ТекДанные.Значение = Результат;
		ТаблицаФормы = Элементы["ТаблицаТЧ_" + ДополнительныеПараметры.ИмяТЧ];
		ТекДанные = ТаблицаФормы.ТекущиеДанные;
		ТекДанные[ДополнительныеПараметры.ИмяПоля] = Результат;
		ИскомаяСтрока = ЭтотОбъект.ТабличныеЧасти.НайтиСтроки(Новый Структура("Имя", ДополнительныеПараметры.ИмяТЧ))[0];
		ИскомаяСтрока.Пометка = Истина;
	ИначеЕсли ДополнительныеПараметры.Источник = "Реквизиты" Тогда
		ТекДанные = Элементы.РеквизитыОбъекта.ТекущиеДанные;
		ТекДанные.Значение = Результат;
		ТекДанные.Пометка = Истина;
	Иначе                                              
		ТаблицаФормы = Элементы["ТаблицаТЧ_" + ДополнительныеПараметры.ИмяТЧ];
		ТекДанные = ТаблицаФормы.ТекущиеДанные;
		ТекДанные[ДополнительныеПараметры.ИмяПоля] = Результат;
		ИскомаяСтрока = ЭтотОбъект.ТабличныеЧасти.НайтиСтроки(Новый Структура("Имя", ДополнительныеПараметры.ИмяТЧ))[0];
		ИскомаяСтрока.Пометка = Истина;
		Если ЭтотОбъект.Настройки.НастройкаВыводитьРеквизитыСтроки Тогда
			ЗаполнитьРеквизитыСтроки("ТаблицаФормы");
		КонецЕсли;
	КонецЕсли;
	
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыборЗначения_МассивТиповФильтр(МассивТипов)
	
	Результат = Новый Массив();
	
	Для Каждого Тип Из МассивТипов Цикл
		Если ЭтоСсылка(Тип) Или Тип = Тип("УникальныйИдентификатор") Или Тип = Тип("Строка") Тогда
			Результат.Добавить(Тип);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыборЗначения_ОткрытьЗначение(Источник = "")
	
	ДополнительныеПараметры = ВыборЗначения_ПараметрыРедактирования();
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Источник = "РеквизитыСтроки" Тогда
		ТекДанные = Элементы.РеквизитыСтроки.ТекущиеДанные;
		Значение = ТекДанные.Значение;
	ИначеЕсли ДополнительныеПараметры.Источник = "Реквизиты" Тогда
		ТекДанные = Элементы.РеквизитыОбъекта.ТекущиеДанные;
		Значение = ТекДанные.Значение;
	ИначеЕсли ДополнительныеПараметры.Источник = "ТЧ" Тогда                                              
		ТаблицаФормы = Элементы["ТаблицаТЧ_" + ДополнительныеПараметры.ИмяТЧ];
		ТекДанные = ТаблицаФормы.ТекущиеДанные;
		Значение = ТекДанные[ДополнительныеПараметры.ИмяПоля];
	Иначе
		Возврат;
	КонецЕсли;
	
	ЗначениеТип = ТипЗнч(Значение);
	
	Если ЗначениеТип = Тип("Строка") Тогда
		Оповещение = Новый ОписаниеОповещения("ВыборЗначения_ОткрытьЗначениеЗавершение", ЭтотОбъект, Новый Структура());
		ПоказатьВводСтроки(Оповещение, Значение, "Строка",, ДополнительныеПараметры.МногострочныйРежим);
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоСсылка(ЗначениеТип) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Описание,Настройки,Ссылка");
	ПараметрыОткрытия.Описание = ОписаниеОбъектаМетаданных(ЗначениеТип);
	ПараметрыОткрытия.Настройки = ЭтотОбъект.Настройки;
	ПараметрыОткрытия.Ссылка = Значение;
	
	ОткрытьФорму(ИмяФормыОбработки("ФормаОбъекта"), ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборЗначения_ОткрытьЗначениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

&НаКлиенте
Функция ВыборЗначения_ПараметрыРедактирования(Источник = "")
	
	Если Источник = "РеквизитыСтроки" Тогда
		ТекДанные = Элементы.РеквизитыСтроки.ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		ИмяТЧ = ЭтотОбъект.ТекИмяТЧ;
		ИмяПоля = ТекДанные.Имя;
		Значение = ТекДанные.Значение;
		ЭлемОписание = ЭтотОбъект.Описание.ТЧ.ГруппыРеквизитовСтруктура[ИмяТЧ][ИмяПоля];
		ОписаниеТипов = ЭлемОписание.Тип;
		МногострочныйРежим = (ЭлемОписание.Свойство("МногострочныйРежим") И ЭлемОписание.МногострочныйРежим);
		МногострочныйРежим = (МногострочныйРежим Или ОписаниеТипов.КвалификаторыСтроки.Длина = 0);
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРеквизитыОбъекта Тогда
		Источник = "Реквизиты";
		ТекДанные = Элементы.РеквизитыОбъекта.ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		ИмяПоля = ТекДанные.Имя;
		Значение = ТекДанные.Значение;
		ЭлемОписание = ЭтотОбъект.Описание.Реквизиты.ГруппыРеквизитовСтруктура[ТекДанные.ИмяКоллекции][ИмяПоля];
		ОписаниеТипов = ЭлемОписание.Тип;	
		МногострочныйРежим = (ЭлемОписание.Свойство("МногострочныйРежим") И ЭлемОписание.МногострочныйРежим);
		МногострочныйРежим = (МногострочныйРежим Или ОписаниеТипов.КвалификаторыСтроки.Длина = 0);
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТабличныеЧасти Тогда
		Источник = "ТЧ";
		ИмяТЧ = ЭтотОбъект.ТекИмяТЧ;
		ТаблицаФормы = Элементы["ТаблицаТЧ_" + ИмяТЧ];
		ТекДанные = ТаблицаФормы.ТекущиеДанные;
		Если ТекДанные = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		ИмяПоля = СтрЗаменить(ТаблицаФормы.ТекущийЭлемент.Имя, "ТаблицаТЧ_" + ИмяТЧ, "");
		Значение = ТекДанные[ИмяПоля];	
		ЭлемОписание = ЭтотОбъект.Описание.ТЧ.ГруппыРеквизитовСтруктура[ИмяТЧ][ИмяПоля];
		ОписаниеТипов = ЭлемОписание.Тип;		
		МногострочныйРежим = (ЭлемОписание.Свойство("МногострочныйРежим") И ЭлемОписание.МногострочныйРежим);
		МногострочныйРежим = (МногострочныйРежим Или ОписаниеТипов.КвалификаторыСтроки.Длина = 0);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура("Источник,ИмяТЧ,ИмяПоля,Значение,ОписаниеТипов,МногострочныйРежим", Источник, ИмяТЧ, ИмяПоля, Значение, ОписаниеТипов, МногострочныйРежим);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЭлементыФормы

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКоличествоСтрокТЧ(Знач Форма, Знач ИмяТЧ = "")
	
	Если ИмяТЧ = "" Тогда                                          
		Для Каждого ИскомаяСтрока Из Форма.ТабличныеЧасти Цикл
			ИскомаяСтрока.Пометка = Ложь;
			ИскомаяСтрока.Количество = Форма["ОбъектФормы"][ИскомаяСтрока.Имя].Количество();
		КонецЦикла;
	Иначе
		МассивСтрок = Форма.ТабличныеЧасти.НайтиСтроки(Новый Структура("Имя", ИмяТЧ));
		ИскомаяСтрока = МассивСтрок[0];
		ИскомаяСтрока.Количество = Форма["ОбъектФормы"][ИмяТЧ].Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыТЧ()
	
	Для Каждого ИмяКоллекции Из ЭтотОбъект.Описание.ТЧ.Имена Цикл
		
		НовСтр = ЭтотОбъект.ТабличныеЧасти.Добавить();
		НовСтр.Имя = ИмяКоллекции;
		НовСтр.Картинка = БиблиотекаКартинок.ВложеннаяТаблица;
		
		ГруппаФормы = Элементы.Добавить("ГруппаТЧ_" + ИмяКоллекции, Тип("ГруппаФормы"), Элементы.ГруппаСтраницыТабличныеЧасти);
		ГруппаФормы.Вид = ВидГруппыФормы.Страница;
		ГруппаФормы.Заголовок = ИмяКоллекции;
		ГруппаФормы.ПутьКДаннымЗаголовка = "ОбъектФормы." + ИмяКоллекции + ".КоличествоСтрок";
		ТаблицаФормы = Элементы.Добавить("ТаблицаТЧ_" + ИмяКоллекции, Тип("ТаблицаФормы"), ГруппаФормы);
		ТаблицаФормы.ПутьКДанным = "ОбъектФормы." + ИмяКоллекции;
		ТекКоллекция = ЭтотОбъект.Описание.ТЧ.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекКоллекция Цикл
			Если ЭлемОписание.Тип.СодержитТип(Тип("ХранилищеЗначения")) Тогда
				Продолжить;
			КонецЕсли;
			ПолеФормы = Элементы.Добавить("ТаблицаТЧ_" + ИмяКоллекции + ЭлемОписание.Имя, Тип("ПолеФормы"), ТаблицаФормы);
			ПолеФормы.ПутьКДанным = "ОбъектФормы." + ИмяКоллекции + "." + ЭлемОписание.Имя;
			ПолеФормы.Вид = ВидПоляФормы.ПолеВвода;
			ЭлемОписание = ЭтотОбъект.Описание.ТЧ.ГруппыРеквизитовСтруктура[ИмяКоллекции][ЭлемОписание.Имя];
			МногострочныйРежим = (ЭлемОписание.Свойство("МногострочныйРежим") И ЭлемОписание.МногострочныйРежим);
			Если МногострочныйРежим Тогда
				ПолеФормы.Высота = 1;
			КонецЕсли;
		КонецЦикла;                                                             
		ТаблицаФормы.УстановитьДействие("ПриАктивизацииСтроки", "ТаблицаФормыПриАктивизацииСтроки");
		ТаблицаФормы.УстановитьДействие("ПриИзменении", "ТаблицаФормыПриИзменении");                      
		ТаблицаФормы.УстановитьДействие("ПриОкончанииРедактирования", "ТаблицаФормыПриОкончанииРедактирования");
		ТаблицаФормы.УстановитьДействие("ПриНачалеРедактирования", "ТаблицаФормыПриНачалеРедактирования");
		ТаблицаФормы.УстановитьДействие("ПослеУдаления", "ТаблицаФормыПослеУдаления");
	КонецЦикла;
	
	ЭтотОбъект.КоличествоТЧ = ЭтотОбъект.ТабличныеЧасти.Количество();
			
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	Элементы.ГруппаРеквизитыОбъекта.ПутьКДаннымЗаголовка = "КоличествоРеквизитов";
	Элементы.ГруппаТабличныеЧасти.ПутьКДаннымЗаголовка = "КоличествоТЧ";
	
	Элементы.РеквизитыОбъекта.ИзменятьСоставСтрок = Ложь;
	Элементы.РеквизитыОбъекта.ИзменятьПорядокСтрок = Ложь;
	
	Элементы.РеквизитыОбъектаИмя.ТолькоПросмотр = Истина;
	Элементы.РеквизитыОбъектаСиноним.ТолькоПросмотр = Истина;
	Элементы.РеквизитыОбъектаТипЗначения.ТолькоПросмотр = Истина;
	
	Элементы.РеквизитыСтроки.ИзменятьСоставСтрок = Ложь;
	Элементы.РеквизитыСтроки.ИзменятьПорядокСтрок = Ложь;
	
	Элементы.РеквизитыСтрокиИмя.ТолькоПросмотр = Истина;
	Элементы.РеквизитыСтрокиСиноним.ТолькоПросмотр = Истина;
	Элементы.РеквизитыСтрокиТипЗначения.ТолькоПросмотр = Истина;
	
	Элементы.ТабличныеЧасти.ИзменятьСоставСтрок = Ложь;
	Элементы.ТабличныеЧасти.ИзменятьПорядокСтрок = Ложь;
	Элементы.ТабличныеЧастиПометка.ТолькоПросмотр = Истина;
	Элементы.ТабличныеЧастиИмя.ТолькоПросмотр = Истина;
	Элементы.ТабличныеЧастиКоличество.ТолькоПросмотр = Истина;
	Элементы.ТабличныеЧасти.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	
	Элементы.Ссылка.Видимость = Не ЭтотОбъект.Описание.ЭтоРегистрСведений;
	Элементы.Ссылка.КнопкаВыбора = Ложь;
	Элементы.Ссылка.ТолькоПросмотр = Истина;
	
	Элементы.ОбменДаннымиЗагрузка.Видимость = Не (ЭтотОбъект.Описание.ЭтоРегистрСведений Или ЭтотОбъект.Описание.ЭтоВнешИсточник);
	
	Элементы.RefUUID.ТолькоПросмотр = Истина;
	Элементы.RefUUID.Видимость = Не ЭтотОбъект.Описание.ЭтоРегистрСведений;
	
	Элементы.RefURL.ТолькоПросмотр = Истина;
	
	Элементы.ФормаГруппаПроведение.Видимость = ЭтотОбъект.Описание.ЕстьДвижения;
	Элементы.ФормаГруппаДвижения.Видимость = ЭтотОбъект.Описание.ЕстьДвижения;                                       
	
	ЕстьТЧ = (ЭтотОбъект.Описание.ТЧ.Имена.Количество() > 0);
	Элементы.ГруппаТабличныеЧасти.Видимость = ЕстьТЧ;
	
	Элементы.РеквизитыСтроки.Видимость = ЕстьТЧ И ЭтотОбъект.Настройки.НастройкаВыводитьРеквизитыСтроки;
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();	
	ПолеЭлемента = ЭлементУО.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РеквизитыСтрокиЗначение.Имя);	
	ОтборЭлемента = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РеквизитыСтроки.Имя");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "НомерСтроки";
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УпрФормой(Знач Форма)
	
	Форма.Элементы.ФормаКомандаЗаписать.Доступность = Форма.Элементы.ФормаКомандаРедактироватьОбъект.Пометка;
	
	Форма.Элементы.ФормаКомандаПровести.Доступность = (Форма.Описание.ЕстьДвижения И Форма.Элементы.ФормаКомандаРедактироватьОбъект.Пометка);
	Форма.Элементы.ФормаКомандаОтменаПроведения.Доступность = (Форма.Описание.ЕстьДвижения И Форма.Элементы.ФормаКомандаРедактироватьОбъект.Пометка И Форма["ОбъектФормы"].Проведен); 
	
КонецПроцедуры

#КонецОбласти

#Область Форма

&НаКлиенте
Процедура ВопросОСохранении_Завершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		ЭтотОбъект.Модифицированность = Ложь;
		ЭтотОбъект.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИмяФормыОбработки(Имя)
	
	ТипМетаданных = ?(СтрНачинаетсяС(ЭтотОбъект.ИмяФормы, "ВнешняяОбработка"), "ВнешняяОбработка", "Обработка");
	Возврат ТипМетаданных + ".bm_Навигатор.Форма." + Имя;
	
КонецФункции

&НаСервере
Функция ОписаниеОбъектаМетаданных(Знач Ссылка)
	
	Обт = РеквизитФормыВЗначение("Объект");
	Результат = Обт.ОписаниеОбъектаМетаданных(Ссылка);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Библиотека

&НаСервереБезКонтекста
Функция ЭтоСсылка(Тип)
	
	Если Тип = Тип("Неопределено") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Результат = Справочники.ТипВсеСсылки().СодержитТип(Тип)
		Или Перечисления.ТипВсеСсылки().СодержитТип(Тип)
		Или Документы.ТипВсеСсылки().СодержитТип(Тип)	
		Или ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(Тип)
		Или ПланыСчетов.ТипВсеСсылки().СодержитТип(Тип)
		Или ПланыВидовРасчета.ТипВсеСсылки().СодержитТип(Тип)
		Или БизнесПроцессы.ТипВсеСсылки().СодержитТип(Тип)
		Или БизнесПроцессы.ТипВсеСсылкиТочекМаршрутаБизнесПроцессов().СодержитТип(Тип)
		Или Задачи.ТипВсеСсылки().СодержитТип(Тип)
		Или ПланыОбмена.ТипВсеСсылки().СодержитТип(Тип);
		
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СкопироватьСтруктуру(Знач Источник)
	
	Приемник = Новый Структура();
	Для Каждого КиЗ Из Источник Цикл
		Приемник.Вставить(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла;
	
	Возврат Приемник;
	
КонецФункции

#КонецОбласти










