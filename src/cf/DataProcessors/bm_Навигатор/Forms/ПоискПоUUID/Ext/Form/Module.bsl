
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.Описание = Параметры.Описание;
	ЭтотОбъект.Настройки = Параметры.Настройки;
	
	Элементы.Ссылка.РедактированиеТекста = Ложь;
	Элементы.Ссылка.КнопкаВыбора = Ложь;
	Элементы.Ссылка.КнопкаВыпадающегоСписка = Ложь;
	Элементы.Ссылка.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	
	УпрФормой(ЭтотОбъект, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура RefUUIDПриИзменении(Элемент)
		
	ВыполнитьПоиск("UUID");
	
	ЕстьСсылка = ЗначениеЗаполнено(ЭтотОбъект.Ссылка) И ТипЗнч(ЭтотОбъект.Ссылка) <> Тип("Строка");
	УпрФормой(ЭтотОбъект, ЕстьСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура RefURLПриИзменении(Элемент)
	
	ВыполнитьПоиск("URL");
	
	ЕстьСсылка = ЗначениеЗаполнено(ЭтотОбъект.Ссылка) И ТипЗнч(ЭтотОбъект.Ссылка) <> Тип("Строка");
	УпрФормой(ЭтотОбъект, ЕстьСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаОткрытие(Элемент, СтандартнаяОбработка)

	Если Не ЭтотОбъект.Настройки.НастройкаОткрыватьФормуОбработки Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = Новый Структура("Описание,Настройки,Ссылка", ЭтотОбъект.Описание, ЭтотОбъект.Настройки, ЭтотОбъект.Ссылка);
	ОткрытьФорму(ИмяФормыОбработки("ФормаОбъекта"), ПараметрыОткрытия, ЭтотОбъект.ВладелецФормы);
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНайти(Команда)
	
	ВыполнитьПоиск("UUID");
	
	ЕстьСсылка = ЗначениеЗаполнено(ЭтотОбъект.Ссылка) И ТипЗнч(ЭтотОбъект.Ссылка) <> Тип("Строка");
	УпрФормой(ЭтотОбъект, ЕстьСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьВСписке(Команда)
	
	Оповестить("bmНавигатор_ПоказатьВСписке", ЭтотОбъект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьЭлемент(Команда)
	
	Если Команда.Имя = "КомандаОткрытьФормуОбработки" Тогда
		ПараметрыОткрытия = Новый Структура("Описание,Настройки,Ссылка", ЭтотОбъект.Описание, ЭтотОбъект.Настройки, ЭтотОбъект.Ссылка);
		ОткрытьФорму(ИмяФормыОбработки("ФормаОбъекта"), ПараметрыОткрытия, ЭтотОбъект.ВладелецФормы);
	Иначе                                      
		ПоказатьЗначение(, ЭтотОбъект.Ссылка);
	КонецЕсли;
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоиск(ИскатьПо)
	
	Если ИскатьПо = "UUID" Тогда		
		ВыполнитьПоискПоUUID();			
	Иначе
		ВыполнитьПоискПоURL();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоискПоUUID()
	
	ReqUUID = Неопределено;
	
	Попытка
		ReqUUID = Новый УникальныйИдентификатор(СокрЛП(ЭтотОбъект.RefUUID));
	Исключение
		ЭтотОбъект.Ссылка = "Неверный UUID";
		ЭтотОбъект.RefURL = "";
		Возврат;
	КонецПопытки;
	
	РезультатПоиска = НайтиПоUUID(ReqUUID);	
	ЭтотОбъект.Ссылка = РезультатПоиска.Ссылка;
	ЭтотОбъект.RefURL = ПолучитьНавигационнуюСсылку(ЭтотОбъект.Ссылка);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоискПоURL()
	
	Если Не СтрНачинаетсяС(ЭтотОбъект.RefURL, "e1cib/data") Тогда
		ЭтотОбъект.Ссылка = "Неподходящий URL";
		ЭтотОбъект.RefUUID = "";
		Возврат;
	ИначеЕсли СтрНачинаетсяС(ЭтотОбъект.RefURL, "e1cib/data/РегистрСведений") Тогда
		ЭтотОбъект.Ссылка = "Неподходящий тип";
		ЭтотОбъект.RefUUID = "";
		Возврат;
	ИначеЕсли СтрНачинаетсяС(ЭтотОбъект.RefURL, "e1cib/data/ВнешнийИсточникДанных") Тогда
		ЭтотОбъект.Ссылка = "Неподходящий тип";
		ЭтотОбъект.RefUUID = "";
		Возврат;
	КонецЕсли;
	
	Попытка
		РезультатПоиска = НайтиПоURL(ЭтотОбъект.RefURL);
	Исключение
		ЭтотОбъект.Ссылка = "Неверный URL";
		ЭтотОбъект.RefUUID = "";
		Возврат;
	КонецПопытки;
	
	Если ТипЗнч(РезультатПоиска.Ссылка) <> Тип(ЭтотОбъект.Описание.ИмяЕдч + "Ссылка." + ЭтотОбъект.Описание.Имя) Тогда
		ЭтотОбъект.Ссылка = "Неподходящий тип";
		ЭтотОбъект.RefUUID = "";
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Ссылка = РезультатПоиска.Ссылка;
	ЭтотОбъект.RefUUID = ЭтотОбъект.Ссылка.УникальныйИдентификатор();
		
КонецПроцедуры

&НаСервере
Функция НайтиПоUUID(Знач ReqUUID)

	Обт = РеквизитФормыВЗначение("Объект");
	Результат = Обт.НайтиПоUUID(ЭтотОбъект.Описание, ReqUUID);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция НайтиПоURL(Знач ReqURL)

	Обт = РеквизитФормыВЗначение("Объект");
	Результат = Обт.НайтиПоURL(ReqURL);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УпрФормой(Знач Форма, ЕстьСсылка)
	
	Форма.Элементы.ФормаКомандаПоказатьВСписке.Доступность = ЕстьСсылка;
	Форма.Элементы.ФормаОткрыть.Доступность = ЕстьСсылка;
	
КонецПроцедуры

&НаКлиенте
Функция ИмяФормыОбработки(Имя)
	
	ТипМетаданных = ?(СтрНачинаетсяС(ЭтотОбъект.ИмяФормы, "ВнешняяОбработка"), "ВнешняяОбработка", "Обработка");
	Возврат ТипМетаданных + ".bm_Навигатор.Форма." + Имя;
	
КонецФункции












