
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Для Каждого Элем Из Параметры.МассивНастроек Цикл
		НовСтр = ЭтотОбъект.ТаблицаНастроек.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Элем);
	КонецЦикла;
	
	Если Параметры.Свойство("СписокПользователей") Тогда
		ЭтотОбъект.СписокПользователей.ЗагрузитьЗначения(Параметры.СписокПользователей);
	КонецЕсли;
	
	ЭтотОбъект.Команда = Параметры.Команда;
	
	Элементы.ТаблицаНастроек.ИзменятьСоставСтрок = Ложь;
	Элементы.ТаблицаНастроек.ИзменятьПорядокСтрок = Ложь;
	Элементы.ТаблицаНастроек.ТолькоПросмотр = Истина;
	
	Если ЭтотОбъект.Команда = "Удалить" Тогда
		ЭтотОбъект.Заголовок = "Удаление настроек";
		Элементы.ФормаКомандаУдалитьНастройки.Заголовок = "Подтвердить удаление";
		Элементы.ФормаКомандаУдалитьНастройки.КнопкаПоУмолчанию = Истина;
		Элементы.ПрименитьКоВсемПользователям.Заголовок = "Удалить настройки всех пользователей";
	ИначеЕсли ЭтотОбъект.Команда = "Скопировать" Тогда
		ЭтотОбъект.Заголовок = "Копирование настроек";
		Элементы.ФормаКомандаСкопироватьНастройки.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Элементы.ВыбранныеПользователи.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	Элементы.ВыбранныеПользователиЗначение.ТолькоПросмотр = Истина;
	Элементы.СписокПользователей.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	Элементы.СписокПользователей.ТолькоПросмотр = Истина;
	
	Элементы.ГруппаВыбранныеПользователи.Видимость = (ЭтотОбъект.Команда = "Скопировать");
	Элементы.ФормаКомандаУдалитьНастройки.Видимость = (ЭтотОбъект.Команда = "Удалить");
	Элементы.ПрименитьКоВсемПользователям.Видимость = (ЭтотОбъект.Команда = "Удалить");
	Элементы.ФормаКомандаСкопироватьНастройки.Видимость = (ЭтотОбъект.Команда = "Скопировать");
	ЭтотОбъект.АвтоЗаголовок = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПользователейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.СписокПользователей.ТекущиеДанные;
	Если ЭтотОбъект.ВыбранныеПользователи.НайтиПоЗначению(ТекДанные.Значение) = Неопределено Тогда
		ЭтотОбъект.ВыбранныеПользователи.Добавить(ТекДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьПользователей(Команда)
	
	Для Каждого Идентификатор Из Элементы.СписокПользователей.ВыделенныеСтроки Цикл
		ЭлементСписка = ЭтотОбъект.СписокПользователей.НайтиПоИдентификатору(Идентификатор);
		Если ЭтотОбъект.ВыбранныеПользователи.НайтиПоЗначению(ЭлементСписка.Значение) = Неопределено Тогда
			ЭтотОбъект.ВыбранныеПользователи.Добавить(ЭлементСписка.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбратьВсехПользователей(Команда)
	
	ЭтотОбъект.ВыбранныеПользователи.ЗагрузитьЗначения(ЭтотОбъект.СписокПользователей.ВыгрузитьЗначения());
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОчиститьВыбранныхПользователей(Команда)
	
	ЭтотОбъект.ВыбранныеПользователи.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьНастройки(Команда)
	
	ЭтотОбъект.Закрыть(Новый Структура("Команда,ПрименитьКоВсемПользователям", ЭтотОбъект.Команда, ЭтотОбъект.ПрименитьКоВсемПользователям));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСкопироватьНастройки(Команда)
	
	Если ЭтотОбъект.ВыбранныеПользователи.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Не выбраны пользователи");
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.Закрыть(Новый Структура("Команда, ВыбранныеПользователи", ЭтотОбъект.Команда, ЭтотОбъект.ВыбранныеПользователи.ВыгрузитьЗначения()));
	
КонецПроцедуры








