&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Диалог = НовыйДиалогСохранения();
	Диалог.ПолноеИмяФайла = ТекДанные.Наименование;
	Оповещение = Новый ОписаниеОповещения("ВыгрузитьЗавершение", ЭтотОбъект, Новый Структура());
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив") Тогда
		Если ВыбранныеФайлы.Количество() > 0 Тогда
			Файл = Новый Файл(ВыбранныеФайлы[0]);
			Если Файл.Расширение = ".bmqval" Тогда
				КонсольЗапросов_СохранитьФайлЗапросов_bmqval(Файл.ПолноеИмя, Элементы.Список.ТекущаяСтрока);
			ИначеЕсли Файл.Расширение = ".bmqxml" Тогда
				КонсольЗапросов_СохранитьФайлЗапросов_bmqxml(Файл.ПолноеИмя, Элементы.Список.ТекущаяСтрока);
			КонецЕсли;			
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КонсольЗапросов_СохранитьФайлЗапросов_bmqval(Знач ПутьКФайлу, Знач КлючЗаписи)
	
	Значение = КонсольЗапросов_ВыгрузитьЗначение_bmqval(КлючЗаписи);
	ЗаписьТекста = Новый ЗаписьТекста(ПутьКФайлу);
	ЗаписьТекста.Записать(Значение);
	ЗаписьТекста.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КонсольЗапросов_СохранитьФайлЗапросов_bmqxml(Знач ПутьКФайлу, Знач КлючЗаписи)
	
	Значение = КонсольЗапросов_ВыгрузитьЗначение_bmqxml(КлючЗаписи);
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ПутьКФайлу);
	ЗаписьXML.ЗаписатьБезОбработки(Значение);
	ЗаписьXML.Закрыть();
	
КонецПроцедуры

&НаСервере
Функция КонсольЗапросов_ВыгрузитьЗначение_bmqval(Знач КлючЗаписи)
	
	СохраняемыеДанные 	= ЗначениеХранилища(КлючЗаписи);
	Значение = ЗначениеВСтрокуВнутр(СохраняемыеДанные);
	
	Возврат Значение;
	
КонецФункции

&НаСервере
Функция КонсольЗапросов_ВыгрузитьЗначение_bmqxml(Знач КлючЗаписи)
	
	СохраняемыеДанные = ЗначениеХранилища(КлючЗаписи);
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, СохраняемыеДанные);
	
	Возврат ЗаписьXML.Закрыть();	
	
КонецФункции

&НаКлиенте
Функция НовыйДиалогСохранения()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = НСтр("ru = 'Файл запросов *.bmqval|*.bmqval|Файл запросов *.bmqxml|*.bmqxml'");
	
	Возврат Диалог;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеХранилища(КлючЗаписи)
	
	Возврат РегистрыСведений.bm_Запросы.ЗначениеХранилища(КлючЗаписи);
	
КонецФункции


