#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ЭтотОбъект.Описание = Параметры.Описание;
	ЭтотОбъект.Настройки = Параметры.Настройки;
	ЭтотОбъект.ЭтоРежимВыбора = (Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора);
	
	СоздатьДинамическийСписок(ЭтотОбъект.Описание, ЭтотОбъект.Настройки);
	ЭтотОбъект.АвтоЗаголовок = Ложь;
	ЭтотОбъект.Заголовок = ЭтотОбъект.Описание.ОсновнаяТаблица;
	Элементы.ФормаКомандаВыбрать.Видимость = ЭтотОбъект.ЭтоРежимВыбора;
	Элементы.ФормаКомандаВыбрать.КнопкаПоУмолчанию = ЭтотОбъект.ЭтоРежимВыбора;
	Элементы.ФормаКомандаНайтиПоUUID.Видимость = Не (ЭтотОбъект.Описание.ИмяЕдч = "Перечисление" Или ЭтотОбъект.Описание.ЭтоРегистрСведений Или ЭтотОбъект.Описание.ЭтоВнешИсточник);
	Элементы.ФормаГруппаДвижения.Видимость = (ЭтотОбъект.Описание.ЕстьДвижения Или ЭтотОбъект.Описание.ИмяЕдч = "ЖурналДокументов");
	
	Если Параметры.Свойство("Ссылка") Тогда
		Элементы[ЭтотОбъект.ИмяСписка].ТекущаяСтрока = Параметры.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "bmНавигатор_ПоказатьВСписке" Тогда
		Элементы[ЭтотОбъект.ИмяСписка].ТекущаяСтрока = Параметр;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы[ЭтотОбъект.ИмяСписка].ТекущиеДанные;
	
	Если ЭтотОбъект.ЭтоРежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		ЭтотОбъект.Закрыть(ТекДанные.Ссылка);
		Возврат;
	КонецЕсли;
	
	Если Не ЭтотОбъект.Настройки.НастройкаОткрыватьФормуОбработки Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Описание.Свойство("Иерархический") И ЭтотОбъект.Описание.Иерархический И ЭтотОбъект.Описание.ВидИерархии = "ИерархияГруппИЭлементов" И ТекДанные.ЭтоГруппа И Элементы[ЭтотОбъект.ИмяСписка].Отображение = ОтображениеТаблицы.ИерархическийСписок Тогда
		Возврат;	
	КонецЕсли;
	
	Если ЭтотОбъект.Описание.ИмяЕдч = "Перечисление"
		Или (ЭтотОбъект.Описание.ЭтоВнешИсточник И ЭтотОбъект.Описание.ТипДанныхТаблицы <> "ОбъектныеДанные") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = ПараметрыОткрытияОбъекта(ТекДанные);	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуОбъекта(ПараметрыОткрытия);	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекДанные = Элементы[ЭтотОбъект.ИмяСписка].ТекущиеДанные;
	
	Если Не ЭтотОбъект.Настройки.НастройкаОткрыватьФормуОбработки Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Описание.ИмяЕдч = "Перечисление"
		Или (ЭтотОбъект.Описание.ЭтоВнешИсточник И ЭтотОбъект.Описание.ТипДанныхТаблицы <> "ОбъектныеДанные") Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Ссылка", Неопределено);
	Если Копирование Тогда                         
		ПараметрыОткрытия.Вставить("СсылкаКопирования", ПолучитьСсылку(ТекДанные));
	КонецЕсли;
	Если (ЭтотОбъект.Описание.Свойство("ПодчинениеРегистратору") И ЭтотОбъект.Описание.ПодчинениеРегистратору) Или ЭтотОбъект.Описание.ИмяЕдч = "ЖурналДокументов" Тогда
		ПараметрыОткрытия.Вставить("Описание", ОписаниеОбъектаМетаданных(Параметр));		
	Иначе
		ПараметрыОткрытия.Вставить("Описание", ЭтотОбъект.Описание);
	КонецЕсли;
	ПараметрыОткрытия.Вставить("Настройки", ЭтотОбъект.Настройки);	
	ОткрытьФормуОбъекта(ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Если ЭтотОбъект.Описание.ИмяЕдч = "Перечисление"
		Или (ЭтотОбъект.Описание.ЭтоВнешИсточник И ЭтотОбъект.Описание.ТипДанныхТаблицы <> "ОбъектныеДанные") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтотОбъект.Настройки.НастройкаОткрыватьФормуОбработки Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные = Элементы[ЭтотОбъект.ИмяСписка].ТекущиеДанные;
	ПараметрыОткрытия = ПараметрыОткрытияОбъекта(ТекДанные);
	Если ПараметрыОткрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	ОткрытьФормуОбъекта(ПараметрыОткрытия);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ТаблицаФормы = Элементы[ЭтотОбъект.ИмяСписка];
	ТаблицаФормы.СоздатьЭлементыФормыПользовательскихНастроек(Элементы.ГруппаПользовательскихНастроек);
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ТекДанные = Элементы[ЭтотОбъект.ИмяСписка].ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЭтотОбъект.Закрыть(ТекДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьЭлемент(Команда)
	
	ТекДанные = Элементы[ЭтотОбъект.ИмяСписка].ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.Описание.ИмяЕдч = "Перечисление"
		Или (ЭтотОбъект.Описание.ЭтоВнешИсточник И ЭтотОбъект.Описание.ТипДанныхТаблицы <> "ОбъектныеДанные") Тогда
		Возврат;
	КонецЕсли;
		
	Если Команда.Имя = "КомандаОткрытьФормуОбработки" Тогда
	
		ПараметрыОткрытия = ПараметрыОткрытияОбъекта(ТекДанные);	
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуОбъекта(ПараметрыОткрытия);
	Иначе                                      
		Если ЭтотОбъект.Описание.ИмяЕдч = "РегистрСведений" Тогда
			ПараметрыОткрытия = Новый Структура("Ключ", Элементы[ЭтотОбъект.ИмяСписка].ТекущаяСтрока);
			ОткрытьФорму("РегистрСведений." + ЭтотОбъект.Описание.Имя + ".ФормаЗаписи", ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
		Иначе
			ПоказатьЗначение(, Элементы[ЭтотОбъект.ИмяСписка].ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоискПоUUID(Команда)
	
	ОткрытьФорму(ИмяФормыОбработки("ПоискПоUUID"), Новый Структура("Описание, Настройки", ЭтотОбъект.Описание, ЭтотОбъект.Настройки), ЭтотОбъект, УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбработкаДвиженияДокумента(Команда)
	
	ТекДанные = Элементы[ЭтотОбъект.ИмяСписка].ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = ПараметрыОткрытияОбъекта(ТекДанные);
	ОткрытьФорму(ИмяФормыОбработки("ФормаОбработкаДвиженияДокумента"), ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтчетДвиженияДокумента(Команда)

	ТекДанные = Элементы[ЭтотОбъект.ИмяСписка].ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("Документ", ПолучитьСсылку(ТекДанные));
	ОткрытьФорму(ИмяФормыОбработки("ФормаОтчетДвиженияДокумента"), ПараметрыОткрытия, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ДинамическийСписок

&НаСервере
Процедура СоздатьДинамическийСписок(Знач Описание, Знач Настройки)
	
	ЭтотОбъект.ИмяСписка = ?(ЭтотОбъект.ЭтоРежимВыбора, "Выбор", "") + "Список" + СтрЗаменить(Описание.ИмяЕдч, ".", "_") + Описание.Имя;
	
	ДобавляемыеРеквизиты = Новый Массив();
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ЭтотОбъект.ИмяСписка, Новый ОписаниеТипов("ДинамическийСписок")));
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	РеквизитФормы = ЭтотОбъект[ЭтотОбъект.ИмяСписка];
	РеквизитФормы.ПроизвольныйЗапрос = Истина;		    
	Если Описание.ИмяЕдч = "Перечисление" Тогда
		РеквизитФормы.ТекстЗапроса = ТекстЗапросаПеречисление(Описание);
		Для Каждого Элем Из ПармаметрыЗапросаПеречисление(Описание) Цикл
			РеквизитФормы.Параметры.УстановитьЗначениеПараметра(Элем.Имя, Элем.Значение);
		КонецЦикла;
	Иначе
		РеквизитФормы.ТекстЗапроса = ТекстЗапросаОбщий(Описание, Настройки);
	КонецЕсли;
	РеквизитФормы.ОсновнаяТаблица = Описание.ОсновнаяТаблица;
	 
	ТаблицаФормы = Элементы.Добавить(ЭтотОбъект.ИмяСписка, Тип("ТаблицаФормы"));
	ТаблицаФормы.ПутьКДанным = ЭтотОбъект.ИмяСписка;
	ТаблицаФормы.УстановитьДействие("Выбор", "СписокВыбор");                            
	ТаблицаФормы.УстановитьДействие("ПередНачаломДобавления", "СписокПередНачаломДобавления");
	ТаблицаФормы.УстановитьДействие("ПередНачаломИзменения", "СписокПередНачаломИзменения");
	ТаблицаФормы.УстановитьДействие("ПриОбновленииСоставаПользовательскихНастроекНаСервере", "СписокПриОбновленииСоставаПользовательскихНастроекНаСервере");
	
	КоллекцииСвойств = Новый Массив();
	КоллекцииСвойств.Добавить("СтандартныеРеквизиты");
	Если Не СтрНачинаетсяС(Описание.ИмяЕдч, "Регистр") Тогда
		КоллекцииСвойств.Добавить("Реквизиты");
	Иначе
		КоллекцииСвойств.Добавить("Измерения");
		КоллекцииСвойств.Добавить("Ресурсы");
		КоллекцииСвойств.Добавить("Реквизиты");
	КонецЕсли;
	
	Если Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Ссылка")
		И Настройки.НастройкаВыводитьUUID
		И Описание.ИмяЕдч <> "Перечисление"
		И Не СтрНачинаетсяС(Описание.ИмяЕдч, "ВнешнийИсточникДанных") Тогда
		ПолеФормы = Элементы.Добавить("СписокUUID", Тип("ПолеФормы"), ТаблицаФормы);
		ПолеФормы.ПутьКДанным = ЭтотОбъект.ИмяСписка + "." + "UUID";
	КонецЕсли;
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				Продолжить;
			КонецЕсли;
			Если ИмяКоллекции = "СтандартныеРеквизиты" И Не ЭлемОписание.ЭтоШапка Тогда
				Продолжить;
			КонецЕсли;
			Если Не СтрНачинаетсяС(ЭтотОбъект.Описание.ИмяЕдч, "Регистр") И Не СтрНачинаетсяС(Описание.ИмяЕдч, "ВнешнийИсточникДанных") И ИмяКоллекции <> "СтандартныеРеквизиты" Тогда
				Если Настройки.НастройкаПоляСпискаСостав = "Стд" Тогда
					Продолжить;
				ИначеЕсли Настройки.НастройкаПоляСпискаСостав = "Вбр" И СтрНайти(Настройки.НастройкаПоляСпискаВыбранные + ",", ЭлемОписание.Имя + ",") = 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			ПолеФормы = Элементы.Добавить("Список" + ЭлемОписание.Имя, Тип("ПолеФормы"), ТаблицаФормы);
			ПолеФормы.ПутьКДанным = ЭтотОбъект.ИмяСписка + "." + ЭлемОписание.Имя;
			Если ЭлемОписание.Свойство("МногострочныйРежим") И ЭлемОписание.МногострочныйРежим Тогда
				ПолеФормы.Высота = 1;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["СтандартныеРеквизиты"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		Если ЭлемОписание.ЭтоШапка Тогда
			Продолжить;
		КонецЕсли;
		ПолеФормы = Элементы.Добавить("Список" + ЭлемОписание.Имя, Тип("ПолеФормы"), ТаблицаФормы);
		ПолеФормы.ПутьКДанным = ЭтотОбъект.ИмяСписка + "." + ЭлемОписание.Имя;
	КонецЦикла;
	
	Если Описание.ИмяЕдч = "Перечисление" Тогда
		Для Каждого ИмяПоля Из СтрРазделить("Имя,Синоним,Комментарий", ",") Цикл
			ПолеФормы = Элементы.Добавить("Список" + ИмяПоля, Тип("ПолеФормы"), ТаблицаФормы);
			ПолеФормы.ПутьКДанным = ЭтотОбъект.ИмяСписка + "." + ИмяПоля;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаОбщий(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Если Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Ссылка")
		И Настройки.НастройкаВыводитьUUID
		И Описание.ИмяЕдч <> "Перечисление"
		И Не СтрНачинаетсяС(Описание.ИмяЕдч, "ВнешнийИсточникДанных") Тогда
		ТекстДок.ДобавитьСтроку("	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Таблица.Ссылка) КАК UUID,");
	КонецЕсли;
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				Продолжить;
			КонецЕсли;
			Если ИмяКоллекции = "СтандартныеРеквизиты" И Не ЭлемОписание.ЭтоШапка Тогда
				Продолжить;
			КонецЕсли;
			Если Не СтрНачинаетсяС(Описание.ИмяЕдч, "Регистр") И Не СтрНачинаетсяС(Описание.ИмяЕдч, "ВнешнийИсточникДанных") И ИмяКоллекции <> "СтандартныеРеквизиты" Тогда
				Если Настройки.НастройкаПоляСпискаСостав = "Стд" Тогда
					Продолжить;
				ИначеЕсли Настройки.НастройкаПоляСпискаСостав = "Вбр" И СтрНайти(Настройки.НастройкаПоляСпискаВыбранные + ",", ЭлемОписание.Имя + ",") = 0 Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
		КонецЦикла;
	КонецЦикла;
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["СтандартныеРеквизиты"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		Если ЭлемОписание.ЭтоШапка Тогда
			Продолжить;
		КонецЕсли;
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
	КонецЦикла;
	
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	ТекстДок.ДобавитьСтроку("	" + Описание.ОсновнаяТаблица + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаПеречисление(Знач Описание)
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	
	ЗначенияПеречисления = Новый Массив();
	Для Каждого ЗначениеПеречисления Из ОМ.ЗначенияПеречисления Цикл
		НовСтр = Новый Структура("Ссылка,Имя,Синоним,Комментарий");
		ЗаполнитьЗначенияСвойств(НовСтр, ЗначениеПеречисления);	
		НовСтр.Ссылка = Перечисления[Описание.Имя][ЗначениеПеречисления.Имя];
		ЗначенияПеречисления.Добавить(НовСтр);
	КонецЦикла;
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ");
	ТекстДок.ДобавитьСтроку("	Таблица.Ссылка КАК Ссылка,");
	ТекстДок.ДобавитьСтроку("	Таблица.Порядок КАК Порядок,");
	
	Для Каждого ИмяПоля Из СтрРазделить("Имя,Синоним,Комментарий", ",") Цикл
		ТекстДок.ДобавитьСтроку("	ВЫБОР");
		НомерЗначения = 0;
		Для Каждого ЗначениеПеречисления Из ЗначенияПеречисления Цикл
			НомерЗначенияСтрока = Формат(НомерЗначения, "ЧЦ=10; ЧН=0; ЧГ=0");
			ТекстДок.ДобавитьСтроку("	КОГДА Таблица.Ссылка = &Ссылка" + НомерЗначенияСтрока);
			ТекстДок.ДобавитьСтроку("		ТОГДА ВЫРАЗИТЬ(&" + ИмяПоля + НомерЗначенияСтрока + " КАК СТРОКА)");
			НомерЗначения = НомерЗначения + 1;
		КонецЦикла;
		ТекстДок.ДобавитьСтроку("	КОНЕЦ КАК " + ИмяПоля + ",");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	ТекстДок.ДобавитьСтроку("	" + Описание.ОсновнаяТаблица + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ПармаметрыЗапросаПеречисление(Знач Описание)
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	
	ЗначенияПеречисления = Новый Массив();
	Для Каждого ЗначениеПеречисления Из ОМ.ЗначенияПеречисления Цикл
		НовСтр = Новый Структура("Ссылка,Имя,Синоним,Комментарий");
		ЗаполнитьЗначенияСвойств(НовСтр, ЗначениеПеречисления);	
		НовСтр.Ссылка = Перечисления[Описание.Имя][ЗначениеПеречисления.Имя];
		ЗначенияПеречисления.Добавить(НовСтр);
	КонецЦикла;
	
	Результат = Новый Массив();
	
	НомерЗначения = 0;
	Для Каждого ЗначениеПеречисления Из ЗначенияПеречисления Цикл		
		НомерЗначенияСтрока = Формат(НомерЗначения, "ЧЦ=10; ЧН=0; ЧГ=0");
		Результат.Добавить(Новый Структура("Имя,Значение", "Ссылка" + НомерЗначенияСтрока, ЗначениеПеречисления["Ссылка"]));
		НомерЗначения = НомерЗначения + 1;
	КонецЦикла;
	
	Для Каждого ИмяПоля Из СтрРазделить("Имя,Синоним,Комментарий", ",") Цикл
		НомерЗначения = 0;
		Для Каждого ЗначениеПеречисления Из ЗначенияПеречисления Цикл
			НомерЗначенияСтрока = Формат(НомерЗначения, "ЧЦ=10; ЧН=0; ЧГ=0");
			Результат.Добавить(Новый Структура("Имя,Значение", ИмяПоля + НомерЗначенияСтрока, ЗначениеПеречисления[ИмяПоля]));
			НомерЗначения = НомерЗначения + 1;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Форма

&НаКлиенте
Процедура ОткрытьФормуОбъекта(ПараметрыОткрытия)
	
	ОткрытьФорму(ИмяФормыОбработки("ФормаОбъекта"), ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыОткрытияОбъекта(Знач ТекДанные)
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Ссылка", ПолучитьСсылку(ТекДанные));
	Если (ЭтотОбъект.Описание.Свойство("ПодчинениеРегистратору") И ЭтотОбъект.Описание.ПодчинениеРегистратору) Или ЭтотОбъект.Описание.ИмяЕдч = "ЖурналДокументов" Тогда
		ПараметрыОткрытия.Вставить("Описание", ОписаниеОбъектаМетаданных(ПараметрыОткрытия.Ссылка));		
	Иначе
		ПараметрыОткрытия.Вставить("Описание", ЭтотОбъект.Описание);
	КонецЕсли;
	ПараметрыОткрытия.Вставить("Настройки", ЭтотОбъект.Настройки);
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСсылку(Знач ТекДанные)
	
	Если ЭтотОбъект.Описание.ИмяЕдч = "РегистрСведений" И Не ЭтотОбъект.Описание.ПодчинениеРегистратору Тогда
		КлючЗаписи = Новый Структура();
		Для Каждого Элем Из ЭтотОбъект.Описание.КлючЗаписи Цикл
			КлючЗаписи.Вставить(Элем.Ключ, ТекДанные[Элем.Ключ]);
		КонецЦикла;
		Возврат КлючЗаписи;
	ИначеЕсли ЭтотОбъект.Описание.Свойство("ПодчинениеРегистратору") И ЭтотОбъект.Описание.ПодчинениеРегистратору Тогда
		Возврат ТекДанные.Регистратор;
	Иначе
		Возврат ТекДанные.Ссылка;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ИмяФормыОбработки(Имя)
	
	ТипМетаданных = ?(СтрНачинаетсяС(ЭтотОбъект.ИмяФормы, "ВнешняяОбработка"), "ВнешняяОбработка", "Обработка");
	Возврат ТипМетаданных + ".bm_Навигатор.Форма." + Имя;
	
КонецФункции

&НаСервере
Функция ОписаниеОбъектаМетаданных(Знач Ссылка)
	
	Обт = РеквизитФормыВЗначение("Объект");
	Результат = Обт.ОписаниеОбъектаМетаданных(Ссылка);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Библиотека

#КонецОбласти	








