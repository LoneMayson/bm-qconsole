#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.Описание = ОписаниеОбъектаМетаданных(Параметры.Описание);
	
	Таблицы = ТаблицыОбъектаМетаданных(ЭтотОбъект.Описание);
	Для Каждого Элем Из Таблицы Цикл
		НовСтр = ЭтотОбъект.ТаблицыЗапроса.Добавить();
		НовСтр.Таблица = Элем.Значение;
		НовСтр.Картинка = Элем.Картинка;
	КонецЦикла;
	
	Элементы.ТаблицыЗапроса.ИзменятьСоставСтрок = Ложь;
	Элементы.ТаблицыЗапроса.ИзменятьПорядокСтрок = Ложь;
	Элементы.ТаблицыЗапроса.ТолькоПросмотр = Истина;
	Элементы.ТаблицыЗапроса.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	
	ЭтотОбъект.ПеременныеОкружения = Новый Структура();
	ЭтотОбъект.ПеременныеОкружения.Вставить("ЕстьРасширение", Метаданные.ОбщиеМодули.Найти("bm_Запросы") <> Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ПриИзмененииНастроек(Элемент)
	
	ПодключитьОбработчикОжидания("ТаблицыЗапросаПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицыЗапросаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьЗапрос();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицыЗапросаПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ТаблицыЗапросаПриАктивизацииСтрокиОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицыЗапросаПриАктивизацииСтрокиОбработчикОжидания()
	
	ТекДанные = Элементы.ТаблицыЗапроса.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Настройки = Новый Структура("НастройкаВыбиратьРазрешенные,НастройкаВыводитьUUID,НастройкаСоединятьТЧ,НастройкаСоединятьСубконто,НастройкаЗаполнятьПараметры");
	ЗаполнитьЗначенияСвойств(Настройки, ЭтотОбъект);
	
	ТекстЗапроса = ТекстЗапросаТаблицы(ЭтотОбъект.Описание, Настройки, ТекДанные.Таблица);
	ЭтотОбъект.РеквизитТекстЗапроса.УстановитьТекст(ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбратьЗапрос();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ТекстЗапроса

&НаСервереБезКонтекста
Функция ТекстЗапросаТаблицы(Знач Описание, Знач Настройки, Знач Таблица)
	
	ТекстЗапроса = "";
	
	Если Описание.ИмяЕдч = "Константа" Тогда
		ТекстЗапроса = ТекстЗапросаОбщий(Описание, Настройки);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Если СтрНачинаетсяС(Таблица, "ТЧ.") Тогда
		ТекстЗапроса = ТекстЗапросаТЧ(Описание, Настройки, Таблица);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Если ЭтоОбычнаяТаблица(Описание) Тогда
		ТекстЗапроса = ТекстЗапросаОбщий(Описание, Настройки);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Если Описание.ИмяЕдч = "РегистрСведений" Тогда
		Если Таблица = "Основная" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрСведенийОсновная(Описание, Настройки);
		Иначе
			ТекстЗапроса = ТекстЗапросаСрез(Описание, Настройки, Таблица);
		КонецЕсли;
	ИначеЕсли Описание.ИмяЕдч = "РегистрНакопления" Тогда
		Если Таблица = "Основная" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрНакопленияОсновная(Описание, Настройки);
		ИначеЕсли Таблица = "Обороты" Тогда
			ТекстЗапроса = ТекстЗапросаОбороты(Описание, Настройки);
		ИначеЕсли Таблица = "Остатки" Тогда
			ТекстЗапроса = ТекстЗапросаОстатки(Описание, Настройки);
		ИначеЕсли Таблица = "ОстаткиИОбороты" Тогда
			ТекстЗапроса = ТекстЗапросаОстаткиИОбороты(Описание, Настройки);
		КонецЕсли;
	ИначеЕсли Описание.ИмяЕдч = "РегистрБухгалтерии" Тогда
		Если Таблица = "Основная" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрБухгалтерииОсновная(Описание, Настройки);
		ИначеЕсли Таблица = "ДвиженияССубконто" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрБухгалтерииДвиженияССубконто(Описание, Настройки);
		ИначеЕсли Таблица = "Обороты" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрБухгалтерииОбороты(Описание, Настройки);
		ИначеЕсли Таблица = "ОборотыДтКт" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрБухгалтерииОборотыДтКт(Описание, Настройки);
		ИначеЕсли Таблица = "Остатки" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрБухгалтерииОстатки(Описание, Настройки);
		ИначеЕсли Таблица = "ОстаткиИОбороты" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрБухгалтерииОстаткиИОбороты(Описание, Настройки);
		ИначеЕсли Таблица = "Субконто" Тогда
			ТекстЗапроса = ТекстЗапросаРегистрБухгалтерииСубконто(Описание, Настройки);
		КонецЕсли;
	ИначеЕсли Описание.ИмяЕдч = "РегистрРасчета" Тогда
		ТекстЗапроса = ТекстЗапросаРегистрРасчета(Описание, Настройки, Таблица);
	КонецЕсли;		
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТаблицыОбъектаМетаданных(Знач Описание) Экспорт
	
	Результат = Новый СписокЗначений();
	
	Если Не ЕстьТаблица(Описание) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если СтрЗаканчиваетсяНа(Описание.ИмяЕдч, ".Таблица") Тогда
		Картинка = БиблиотекаКартинок.ВнешнийИсточникДанныхТаблица;
	Иначе
		Картинка = БиблиотекаКартинок[Описание.ИмяЕдч];
	КонецЕсли;
	
	Результат.Добавить("Основная",,, Картинка);
	Для Каждого ИмяКоллекции Из Описание.ТЧ.Имена Цикл
		Результат.Добавить("ТЧ." + ИмяКоллекции,,, БиблиотекаКартинок.ВложеннаяТаблица);	
	КонецЦикла;
	
	Если Не СтрНачинаетсяС(Описание.ИмяЕдч, "Регистр") Тогда
		Возврат Результат;
	КонецЕсли;
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	Если Описание.ИмяЕдч = "РегистрСведений" Тогда
		Если ОМ.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			Результат.Добавить("СрезПоследних",,, Картинка);
			Результат.Добавить("СрезПервых",,, Картинка);
		КонецЕсли;
	ИначеЕсли Описание.ИмяЕдч = "РегистрНакопления" Тогда
		Результат.Добавить("Обороты",,, Картинка);
		Если ОМ.ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
			Результат.Добавить("Остатки",,, Картинка);
			Результат.Добавить("ОстаткиИОбороты",,, Картинка);
		КонецЕсли;
	ИначеЕсли Описание.ИмяЕдч = "РегистрБухгалтерии" Тогда
		Результат.Добавить("ДвиженияССубконто",,, Картинка);
		Результат.Добавить("Обороты",,, Картинка);
		Если ОМ.Корреспонденция = Истина Тогда
			Результат.Добавить("ОборотыДтКт",,, Картинка);
		КонецЕсли;
		Результат.Добавить("Остатки",,, Картинка);
		Результат.Добавить("ОстаткиИОбороты",,, Картинка);
		Результат.Добавить("Субконто",,, Картинка);
	ИначеЕсли Описание.ИмяЕдч = "РегистрРасчета" Тогда
		СхемаЗапроса = Новый СхемаЗапроса();
		ДоступныеТаблицы = СхемаЗапроса.ПакетЗапросов[0].ДоступныеТаблицы;
		ИскомаяСтрока = Неопределено;
		Для Каждого Элем из ДоступныеТаблицы Цикл
			Если Элем.Представление = "РегистрыРасчета" Тогда
				ИскомаяСтрока = Элем;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ИскомаяСтрока <> Неопределено Тогда
			Для Каждого Элем Из ИскомаяСтрока.Состав Цикл
				Если СтрНачинаетсяС(Элем.Имя, "РегистрРасчета." + Описание.Имя) Тогда
					Если СтрЗаканчиваетсяНа(Элем.Имя, "ДанныеГрафика") Тогда
						Результат.Добавить("ДанныеГрафика",,, Картинка);
					ИначеЕсли СтрЗаканчиваетсяНа(Элем.Имя, "ФактическийПериодДействия") Тогда
						Результат.Добавить("ФактическийПериодДействия",,, Картинка);
					ИначеЕсли СтрНачинаетсяС(Элем.Имя, "РегистрРасчета." + Описание.Имя + ".База") Тогда
						МассивСтрок = СтрРазделить(Элем.Имя, ".");
						Результат.Добавить(МассивСтрок[МассивСтрок.ВГраница()],,, Картинка);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаОбщий(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Если Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Ссылка")
		И Настройки.НастройкаВыводитьUUID
		И Описание.ИмяЕдч <> "Перечисление"
		И Не СтрНачинаетсяС(Описание.ИмяЕдч, "ВнешнийИсточникДанных") Тогда
		ТекстДок.ДобавитьСтроку("	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Таблица.Ссылка) КАК UUID,");
	КонецЕсли;
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				ТекстДок.ДобавитьСтроку("	""<Хранилище значения>"" КАК " + ЭлемОписание.Имя + ",");
				Продолжить;
			КонецЕсли;
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
		КонецЦикла;
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	ТекстДок.ДобавитьСтроку("	" + Описание.ОсновнаяТаблица + " КАК Таблица");
	
	Если Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Ссылка")
		И Описание.ИмяЕдч <> "Перечисление"
		И Не СтрНачинаетсяС(Описание.ИмяЕдч, "ВнешнийИсточникДанных") Тогда
		ТекстДок.ДобавитьСтроку("ГДЕ");
		ТекстДок.ДобавитьСтроку("	Таблица.Ссылка = &Ссылка");
	КонецЕсли;
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаТЧ(Знач Описание, Знач Настройки, Знач Таблица)
	
	ИмяТЧ = СтрЗаменить(Таблица, "ТЧ.", "");
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Если Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Ссылка")
		И Настройки.НастройкаВыводитьUUID
		И Описание.ИмяЕдч <> "Перечисление" Тогда
		ТекстДок.ДобавитьСтроку("	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Таблица.Ссылка) КАК UUID,");
	КонецЕсли;
	
	Для Каждого ЭлемОписание Из Описание.ТЧ.ГруппыРеквизитовМассив[ИмяТЧ] Цикл
		Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
			ТекстДок.ДобавитьСтроку("	""<Хранилище значения>"" КАК " + ЭлемОписание.Имя + ",");
			Продолжить;
		КонецЕсли;
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	Если Не Настройки.НастройкаСоединятьТЧ Тогда
		ТекстДок.ДобавитьСтроку("	" + Описание.ОсновнаяТаблица + "." + ИмяТЧ + " КАК Таблица");
	Иначе
		ТекстДок.ДобавитьСтроку("	" + Описание.ОсновнаяТаблица + " КАК Шапка");
		ТекстДок.ДобавитьСтроку("		" + "ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + Описание.ОсновнаяТаблица + "." + ИмяТЧ + " КАК Таблица");
		ТекстДок.ДобавитьСтроку("		ПО (Шапка.Ссылка = Таблица.Ссылка)");		
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("ГДЕ");
	ТекстДок.ДобавитьСтроку("	Таблица.Ссылка = &Ссылка");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрСведенийОсновная(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				ТекстДок.ДобавитьСтроку("	""<Хранилище значения>"" КАК " + ЭлемОписание.Имя + ",");
				Продолжить;
			КонецЕсли;
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
		КонецЦикла;
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	ТекстДок.ДобавитьСтроку("	" + Описание.ОсновнаяТаблица + " КАК Таблица");
	
	Если Описание.Реквизиты.ГруппыРеквизитовСтруктура.СтандартныеРеквизиты.Свойство("Регистратор") Тогда
		ТекстДок.ДобавитьСтроку("ГДЕ");
		ТекстДок.ДобавитьСтроку("	Таблица.Регистратор = &Регистратор");
	КонецЕсли;
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаСрез(Знач Описание, Знач Настройки, Знач Таблица)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Имя = "НомерСтроки" Или ЭлемОписание.Имя = "Активность" Тогда
				Продолжить;
			КонецЕсли;
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				ТекстДок.ДобавитьСтроку("	""<Хранилище значения>"" КАК " + ЭлемОписание.Имя + ",");
				Продолжить;
			КонецЕсли;
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
		КонецЦикла;
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	
	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		ЧастьЗапроса = "(&ДатаСведений, " + Текст + ")";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + "." + Таблица + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрНакопленияОсновная(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				ТекстДок.ДобавитьСтроку("	""<Хранилище значения>"" КАК " + ЭлемОписание.Имя + ",");
				Продолжить;
			КонецЕсли;
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
		КонецЦикла;
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	ТекстДок.ДобавитьСтроку("	" + Описание.ОсновнаяТаблица + " КАК Таблица");
	
	ТекстДок.ДобавитьСтроку("ГДЕ");
	ТекстДок.ДобавитьСтроку("	Таблица.Регистратор = &Регистратор");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаОбороты(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
	КонецЦикла;
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["Ресурсы"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "Оборот КАК " + ЭлемОписание.Имя + "Оборот,");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	
	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		ЧастьЗапроса = "(&ДатаНачала, &ДатаОкончания, Период, " + Текст + ")";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".Обороты" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаОстатки(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
	КонецЦикла;
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["Ресурсы"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "Остаток КАК " + ЭлемОписание.Имя + "Остаток,");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	
	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, , , )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		ЧастьЗапроса = "(&ГраницаОстатков, " + Текст + ")";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".Остатки" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаОстаткиИОбороты(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
	КонецЦикла;
	
	ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив["Ресурсы"];
	Для Каждого ЭлемОписание Из ТекМассив Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "НачальныйОстаток КАК " + ЭлемОписание.Имя + "НачОст,");
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "Приход КАК " + ЭлемОписание.Имя + "Приход,");
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "Расход КАК " + ЭлемОписание.Имя + "Расход,");
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "КонечныйОстаток КАК " + ЭлемОписание.Имя + "КонОст,");
		ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "Оборот КАК " + ЭлемОписание.Имя + "Оборот,");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	
	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, , , , )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		ЧастьЗапроса = "(&ДатаНачала, &ДатаОкончания, Период, ДвиженияИГраницыПериода, " + Текст + ")";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".ОстаткиИОбороты" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрБухгалтерииОсновная(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Если ИмяКоллекции = "Счет" Или ИмяКоллекции = "СчетДт" Или ИмяКоллекции = "СчетКт" Тогда
			ТекМассив = Новый Массив();
			ТекМассив.Добавить(Новый Структура("Имя", ИмяКоллекции));
		КонецЕсли; 
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				ТекстДок.ДобавитьСтроку("	""<Хранилище значения>"" КАК " + ЭлемОписание.Имя + ",");
				Продолжить;
			КонецЕсли;
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
		КонецЦикла;
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + " КАК Таблица");
	
	ТекстДок.ДобавитьСтроку("ГДЕ");
	ТекстДок.ДобавитьСтроку("	Таблица.Регистратор = &Регистратор");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрБухгалтерииДвиженияССубконто(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	Для Каждого ИмяКоллекции Из Описание.Реквизиты.Имена Цикл
		ТекМассив = Описание.Реквизиты.ГруппыРеквизитовМассив[ИмяКоллекции];
		Для Каждого ЭлемОписание Из ТекМассив Цикл
			Если ЭлемОписание.Свойство("ЭтоХранилищеЗначения") И ЭлемОписание.ЭтоХранилищеЗначения Тогда
				ТекстДок.ДобавитьСтроку("	""<Хранилище значения>"" КАК " + ЭлемОписание.Имя + ",");
				Продолжить;
			КонецЕсли;
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + " КАК " + ЭлемОписание.Имя + ",");
		КонецЦикла;
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");

	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, , , , )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		ЧастьЗапроса = "(&ДатаНачала, &ДатаОкончания, " + Текст + ", , )";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".ДвиженияССубконто" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрБухгалтерииОбороты(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	
	КоличествоСубконто = 1;
	
	МассивПолей = Новый Массив();

	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если (Описание.Корреспонденция И ЭлемОписание.Балансовый) Или Не Описание.Корреспонденция Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя);
		КонецЕсли;
	КонецЦикла;
	
	МассивПолей.Добавить("Счет");
	
	Для Итер = 1 По КоличествоСубконто Цикл
		МассивПолей.Добавить("Субконто" + Итер);
	КонецЦикла;
	
	Если Описание.Корреспонденция Тогда
		Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
			Если Не ЭлемОписание.Балансовый Тогда
				МассивПолей.Добавить(ЭлемОписание.Имя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Описание.Корреспонденция Тогда
		
		МассивПолей.Добавить("КорСчет");
		
		Для Итер = 1 По КоличествоСубконто Цикл
			МассивПолей.Добавить("КорСубконто" + Итер);
		КонецЦикла;
	
	КонецЕсли;
	
	Если Описание.Корреспонденция Тогда
		Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
			Если Не ЭлемОписание.Балансовый Тогда
				МассивПолей.Добавить(ЭлемОписание.Имя + "Кор");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Описание.Корреспонденция Тогда
		Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
			Если Не ЭлемОписание.Балансовый Тогда
				МассивПолей.Добавить(ЭлемОписание.Имя + "Оборот");
				МассивПолей.Добавить(ЭлемОписание.Имя + "ОборотДт");
				МассивПолей.Добавить(ЭлемОписание.Имя + "ОборотКт");
				МассивПолей.Добавить(ЭлемОписание.Имя + "КорОборот");
				МассивПолей.Добавить(ЭлемОписание.Имя + "КорОборотДт");
				МассивПолей.Добавить(ЭлемОписание.Имя + "КорОборотКт");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
		
		Если (Описание.Корреспонденция И ЭлемОписание.Балансовый) Или Не Описание.Корреспонденция Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя + "Оборот");
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОборотДт");
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОборотКт");
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ИмяПоля Из МассивПолей Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ИмяПоля + " КАК " + ИмяПоля + ",");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	
	
	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		Если Описание.Корреспонденция Тогда
			ЧастьЗапроса = "(, , , , , , , )";
		Иначе
			ЧастьЗапроса = "(, , , , , )";
		КонецЕсли;
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		Если Описание.Корреспонденция Тогда
			Текст = Текст + " И Субконто1 = &Субконто1 И КорСубконто1 = &КорСубконто1";
			ЧастьЗапроса = "(&ДатаНачала, &ДатаОкончания, Период, Счет = &Счет, &Субконто, " + Текст + ", КорСчет = &КорСчет, &КорСубконто)";
		Иначе
			Текст = Текст + " И Субконто1 = &Субконто1";
			ЧастьЗапроса = "(&ДатаНачала, &ДатаОкончания, Период, Счет = &Счет, &Субконто, " + Текст + ")";
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".Обороты" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрБухгалтерииОборотыДтКт(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	
	КоличествоСубконто = 1;
	
	МассивПолей = Новый Массив();

	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя);
		КонецЕсли;
	КонецЦикла;
	
	МассивПолей.Добавить("СчетДт");

	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если Не ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя + "Дт");
		КонецЕсли;
	КонецЦикла;
	
	Для Итер = 1 По КоличествоСубконто Цикл
		МассивПолей.Добавить("СубконтоДт" + Итер);
	КонецЦикла;
	
	МассивПолей.Добавить("СчетКт");
	
	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если Не ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя + "Кт");
		КонецЕсли;
	КонецЦикла;
	
	Для Итер = 1 По КоличествоСубконто Цикл
		МассивПолей.Добавить("СубконтоКт" + Итер);
	КонецЦикла;
	
	Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
		
		Если Не ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОборотДт");
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОборотКт");
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
		
		Если ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя + "Оборот");
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ИмяПоля Из МассивПолей Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ИмяПоля + " КАК " + ИмяПоля + ",");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	

	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, , , , , , , )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		Текст = Текст + " И СубконтоДт1 = &СубконтоДт1 И СубконтоКт1 = &СубконтоКт1";
		ЧастьЗапроса = "(&ДатаНачала, &ДатаОкончания, Период, СчетДт = &СчетДт, &СубконтоДт, СчетКт = &СчетКт, &СубконтоКт, " + Текст + ")";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".ОборотыДтКт" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрБухгалтерииОстатки(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	
	КоличествоСубконто = 1;
	
	МассивПолей = Новый Массив();

	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если (Описание.Корреспонденция И ЭлемОписание.Балансовый) Или Не Описание.Корреспонденция Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя);
		КонецЕсли;
	КонецЦикла;
	
	МассивПолей.Добавить("Счет");

	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если Не ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Для Итер = 1 По КоличествоСубконто Цикл
		МассивПолей.Добавить("Субконто" + Итер);
	КонецЦикла;
	
	Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
		
		Если Не ЭлемОписание.Балансовый Тогда                 
			МассивПолей.Добавить(ЭлемОписание.Имя + "Остаток");
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОстатокДт");
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОстатокКт");
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
		
		Если ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя + "Остаток");
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОстатокДт");
			МассивПолей.Добавить(ЭлемОписание.Имя + "ОстатокКт");
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ИмяПоля Из МассивПолей Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ИмяПоля + " КАК " + ИмяПоля + ",");
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	

	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, , , )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		Текст = Текст + " И Субконто1 = &Субконто1";
		ЧастьЗапроса = "(&ГраницаОстатков, Счет = &Счет, &Субконто, " + Текст + ")";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".Остатки" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрБухгалтерииОстаткиИОбороты(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	
	КоличествоСубконто = 1;
	
	МассивПолей = Новый Массив();

	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если (Описание.Корреспонденция И ЭлемОписание.Балансовый) Или Не Описание.Корреспонденция Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя);
		КонецЕсли;
	КонецЦикла;
	
	МассивПолей.Добавить("Счет");

	Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
		Если Не ЭлемОписание.Балансовый Тогда
			МассивПолей.Добавить(ЭлемОписание.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Для Итер = 1 По КоличествоСубконто Цикл
		МассивПолей.Добавить("Субконто" + Итер);
	КонецЦикла;
	
	Для Каждого ИмяПоля Из МассивПолей Цикл
		ТекстДок.ДобавитьСтроку("	Таблица." + ИмяПоля + " КАК " + ИмяПоля + ",");
	КонецЦикла;
	
	Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
		Если Не ЭлемОписание.Балансовый Тогда
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "НачальныйОстаток КАК " + ЭлемОписание.Имя + "НачОст,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "НачальныйОстатокДт КАК " + ЭлемОписание.Имя + "НачОстДт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "НачальныйОстатокКт КАК " + ЭлемОписание.Имя + "НачОстКт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "Оборот КАК " + ЭлемОписание.Имя + "Оборот,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "ОборотДт КАК " + ЭлемОписание.Имя + "ОборотДт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "ОборотКт КАК " + ЭлемОписание.Имя + "ОборотКт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "КонечныйОстаток КАК " + ЭлемОписание.Имя + "КонОст,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "КонечныйОстатокДт КАК " + ЭлемОписание.Имя + "КонОстДт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "КонечныйОстатокКт КАК " + ЭлемОписание.Имя + "КонОстКт,");
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
		Если ЭлемОписание.Балансовый Тогда
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "НачальныйОстаток КАК " + ЭлемОписание.Имя + "НачОст,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "НачальныйОстатокДт КАК " + ЭлемОписание.Имя + "НачОстДт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "НачальныйОстатокКт КАК " + ЭлемОписание.Имя + "НачОстКт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "Оборот КАК " + ЭлемОписание.Имя + "Оборот,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "ОборотДт КАК " + ЭлемОписание.Имя + "ОборотДт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "ОборотКт КАК " + ЭлемОписание.Имя + "ОборотКт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "КонечныйОстаток КАК " + ЭлемОписание.Имя + "КонОст,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "КонечныйОстатокДт КАК " + ЭлемОписание.Имя + "КонОстДт,");
			ТекстДок.ДобавитьСтроку("	Таблица." + ЭлемОписание.Имя + "КонечныйОстатокКт КАК " + ЭлемОписание.Имя + "КонОстКт,");
		КонецЕсли;
	КонецЦикла;
	
	НомерСтроки = ТекстДок.КоличествоСтрок();
	ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
	ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
	ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
	
	ТекстДок.ДобавитьСтроку("ИЗ");
	
	ЧастьЗапроса = "";
	Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
		ЧастьЗапроса = "(, , , , , , )";
	Иначе                 
		Текст = "";
		ТекКоллекция = Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"];
		Количество = ТекКоллекция.Количество();
		Итер = 1;
		Для Каждого ЭлемОписание Из Описание.Реквизиты.ГруппыРеквизитовМассив["Измерения"] Цикл
			Текст = Текст + ЭлемОписание.Имя + " = &" + ЭлемОписание.Имя + ?(Итер < Количество, " И ", "");
			Итер = Итер + 1;
		КонецЦикла;
		Текст = Текст + " И Субконто1 = &Субконто1";
		ЧастьЗапроса = "(&ДатаНачала, &ДатаОкончания, Период, ДвиженияИГраницыПериода, Счет = &Счет, &Субконто, " + Текст + ")";
	КонецЕсли;
	
	ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".ОстаткиИОбороты" + ЧастьЗапроса + " КАК Таблица");
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрБухгалтерииСубконто(Знач Описание, Знач Настройки)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	
	ОМ = Метаданные[Описание.ИмяМнч][Описание.Имя];
	
	МассивПолей = Новый Массив();
	
	Если Не Настройки.НастройкаСоединятьСубконто Тогда
		
		МассивПолей.Добавить("Период");
		МассивПолей.Добавить("Регистратор");
		МассивПолей.Добавить("НомерСтроки");
		Если Описание.Корреспонденция Тогда
			МассивПолей.Добавить("ВидДвижения");
		КонецЕсли;	
		МассивПолей.Добавить("Вид");
		МассивПолей.Добавить("Значение");
		
		Для Каждого ИмяПоля Из МассивПолей Цикл
			ТекстДок.ДобавитьСтроку("	Таблица." + ИмяПоля + " КАК " + ИмяПоля + ",");
		КонецЦикла;
		
		НомерСтроки = ТекстДок.КоличествоСтрок();
		ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
		ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
		ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
		
		ТекстДок.ДобавитьСтроку("ИЗ");
		ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + ".Субконто КАК Таблица");
		
		ТекстДок.ДобавитьСтроку("ГДЕ");
		ТекстДок.ДобавитьСтроку("	Таблица.Регистратор = &Регистратор");
		
	Иначе
		
		МассивПолей.Добавить("Период");
		МассивПолей.Добавить("Регистратор");
		МассивПолей.Добавить("НомерСтроки");
		МассивПолей.Добавить("Активность");
		
		Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
			Если (Описание.Корреспонденция И ЭлемОписание.Балансовый) Или Не Описание.Корреспонденция Тогда
				МассивПолей.Добавить(ЭлемОписание.Имя);
			КонецЕсли;
		КонецЦикла;
	
		Если Не Описание.Корреспонденция Тогда
			МассивПолей.Добавить("Счет");
		Иначе                              
			МассивПолей.Добавить("СчетДт");
			Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
				Если Не ЭлемОписание.Балансовый Тогда
					МассивПолей.Добавить(ЭлемОписание.Имя + "Дт");
				КонецЕсли;
			КонецЦикла;
			МассивПолей.Добавить("СчетКт");
			Для Каждого ЭлемОписание Из ОМ.Измерения Цикл
				Если Не ЭлемОписание.Балансовый Тогда
					МассивПолей.Добавить(ЭлемОписание.Имя + "Кт");
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли;
		
		Если Описание.Корреспонденция Тогда
			Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
				Если Не ЭлемОписание.Балансовый Тогда
					МассивПолей.Добавить(ЭлемОписание.Имя + "Дт");
					МассивПолей.Добавить(ЭлемОписание.Имя + "Кт");
				КонецЕсли;
			КонецЦикла;
			Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
				Если ЭлемОписание.Балансовый Тогда
					МассивПолей.Добавить(ЭлемОписание.Имя);
				КонецЕсли;
			КонецЦикла;
		Иначе
			Для Каждого ЭлемОписание Из ОМ.Ресурсы Цикл
				МассивПолей.Добавить(ЭлемОписание.Имя);
			КонецЦикла;
		КонецЕсли; 
		
		Для Каждого ИмяПоля Из МассивПолей Цикл
			ТекстДок.ДобавитьСтроку("	ТаблицаОсновная." + ИмяПоля + " КАК " + ИмяПоля + ",");
		КонецЦикла;
		
		Если Описание.Корреспонденция Тогда
			ТекстДок.ДобавитьСтроку("	ТаблицаСубконто.ВидДвижения КАК ВидДвижения,");
		КонецЕсли;
		ТекстДок.ДобавитьСтроку("	ТаблицаСубконто.Вид КАК Вид,");
		ТекстДок.ДобавитьСтроку("	ТаблицаСубконто.Значение КАК Значение,");
		
		НомерСтроки = ТекстДок.КоличествоСтрок();
		ТекСтрока = ТекстДок.ПолучитьСтроку(НомерСтроки);
		ТекСтрока = Лев(ТекСтрока, СтрДлина(ТекСтрока) - 1);
		ТекстДок.ЗаменитьСтроку(НомерСтроки, ТекСтрока);
		
		ТекстДок.ДобавитьСтроку("ИЗ");
		ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + " КАК ТаблицаОсновная");
		
		ТекстДок.ДобавитьСтроку("		" + "ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + Описание.ИмяЕдч + "." + Описание.Имя + ".Субконто КАК ТаблицаСубконто");
		ТекстДок.ДобавитьСтроку("		ПО (ТаблицаОсновная.Регистратор = ТаблицаСубконто.Регистратор)");
		ТекстДок.ДобавитьСтроку("			И (ТаблицаОсновная.НомерСтроки = ТаблицаСубконто.НомерСтроки)");
		
		ТекстДок.ДобавитьСтроку("ГДЕ");
		ТекстДок.ДобавитьСтроку("	ТаблицаОсновная.Регистратор = &Регистратор");
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаРегистрРасчета(Знач Описание, Знач Настройки, Знач Таблица)
	
	ТекстДок = Новый ТекстовыйДокумент();
	ТекстДок.ДобавитьСтроку("ВЫБРАТЬ" + ?(Настройки.НастройкаВыбиратьРазрешенные, " РАЗРЕШЕННЫЕ", ""));
	ТекстДок.ДобавитьСтроку("	Таблица.*");
	ТекстДок.ДобавитьСтроку("ИЗ");
	
	Если Таблица = "Основная" Тогда
		ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя + " КАК Таблица");
		ТекстДок.ДобавитьСтроку("ГДЕ");
		ТекстДок.ДобавитьСтроку("	Таблица.Регистратор = &Регистратор");
	ИначеЕсли Таблица = "ДанныеГрафика" Тогда
		ЧастьЗапроса = "()";
		ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя  + "." + Таблица + ЧастьЗапроса + " КАК Таблица");
	ИначеЕсли Таблица = "ФактическийПериодДействия" Тогда
		ЧастьЗапроса = "()";
		ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя  + "." + Таблица + ЧастьЗапроса + " КАК Таблица");
	ИначеЕсли СтрНачинаетсяС(Таблица, "База") Тогда
		Если Не Настройки.НастройкаЗаполнятьПараметры Тогда
			ЧастьЗапроса = "(, , , )";
		Иначе
			ЧастьЗапроса = "(&ИзмеренияОсн, &ИзмеренияБаз, &Разрезы, )";
		КонецЕсли;                                                       
		ТекстДок.ДобавитьСтроку("	" + Описание.ИмяЕдч + "." + Описание.Имя  + "." + Таблица + ЧастьЗапроса + " КАК Таблица");
	КонецЕсли;
	
	ТекстЗапроса = ТекстДок.ПолучитьТекст();
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Форма

&НаКлиенте
Процедура ВыбратьЗапрос()
	
	ТекстЗапроса = ЭтотОбъект.РеквизитТекстЗапроса.ПолучитьТекст();	
	ЭтотОбъект.Закрыть(ТекстЗапроса);
	
КонецПроцедуры

&НаКлиенте
Функция ИмяФормыОбработки(Имя)
	
	ТипМетаданных = ?(СтрНачинаетсяС(ЭтотОбъект.ИмяФормы, "ВнешняяОбработка"), "ВнешняяОбработка", "Обработка");
	Возврат ТипМетаданных + ".bm_Навигатор.Форма." + Имя;
	
КонецФункции

&НаСервере
Функция ОписаниеОбъектаМетаданных(Знач Описание)
	
	Обт = РеквизитФормыВЗначение("Объект");
	Результат = Обт.ОписаниеОбъектаМетаданных(Описание);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Библиотека

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьТаблица(Знач Описание)
	
	Возврат Лев(Описание.ИмяЕдч, 7) = "Регистр"
		Или ЭтоОбычнаяТаблица(Описание);	
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОбычнаяТаблица(Знач Описание)
	
	Возврат Найти("Константа,ПланОбмена,Справочник,Документ,ЖурналДокументов,Перечисление,ПланВидовХарактеристик,ПланСчетов,ПланВидовРасчета,БизнесПроцесс,Задача,ВнешнийИсточникДанных", Описание.ИмяЕдч) > 0
		Или СтрЗаканчиваетсяНа(Описание.ИмяЕдч, ".Таблица");
	
КонецФункции

#КонецОбласти