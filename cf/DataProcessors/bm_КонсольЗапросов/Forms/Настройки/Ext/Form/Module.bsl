
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.ПеременныеОкружения = Новый Структура();
	ЭтотОбъект.ПеременныеОкружения.Вставить("БезопасныйРежим", БезопасныйРежим());
	ЭтотОбъект.ПеременныеОкружения.Вставить("ЭтоКлиентСервер", СтрНайти(ВРег(СтрокаСоединенияИнформационнойБазы()), "FILE=") = 0);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, КонсольЗапросов_СтрокаНастроек());
	Если Метаданные.ОбщиеМодули.Найти("bm_Запросы") = Неопределено
		Или Метаданные.ОбщиеМодули.Найти("ДлительныеОперации") = Неопределено Тогда
		Элементы.НастройкаВыполнятьВФоне.Доступность = Ложь;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФорматЧислаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Конструктор = Новый КонструкторФорматнойСтроки(ЭтотОбъект.НастройкаФорматЧисла);
	Оповещение = Новый ОписаниеОповещения("ОткрытьКонструкторФорматнойСтроки_Завершение", ЭтотОбъект, Новый Структура());
	Конструктор.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонструкторФорматнойСтроки_Завершение(Текст, ДополнительныеПараметры) Экспорт
	
	Если Текст <> Неопределено Тогда
		ЭтотОбъект.НастройкаФорматЧисла = Текст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФиксированнаяШиринаКолонокПриИзменении(Элемент)
	
	Если ЭтотОбъект.НастройкаФиксированнаяШиринаКолонок И ЭтотОбъект.НастройкаШиринаКолонок = 0 Тогда
		ЭтотОбъект.НастройкаШиринаКолонок = 10;
	КонецЕсли;
		
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаШиринаКолонокПриИзменении(Элемент)
	
	Если ЭтотОбъект.НастройкаШиринаКолонок = 0 Тогда
		ЭтотОбъект.НастройкаШиринаКолонок = 10;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура НастройкаРаботаСТЖПриИзменении(Элемент)
	
	Если ЭтотОбъект.НастройкаРаботаСТЖ Тогда
		НастройкаРаботаСТЖПриИзмененииНаСервере();
	Иначе
		ЭтотОбъект.НастройкаТЖПутьКФайлуКонфигурацииЛогов = "";
		ЭтотОбъект.НастройкаТЖКаталогЛогов = "";	
		ЭтотОбъект.НастройкаТЖКаталогТекущихЛогов = "";
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура НастройкаРаботаСТЖПриИзмененииНаСервере()
	
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	НастройкиКонфигурацииЛогов = ОбъектОбработки.РаботаСТЖ_НастройкиКонфигурацииЛогов();
	ЭтотОбъект.НастройкаТЖПутьКФайлуКонфигурацииЛогов = НастройкиКонфигурацииЛогов.ПутьКФайлуКонфигурацииЛогов;
	ЭтотОбъект.НастройкаТЖКаталогЛогов = НастройкиКонфигурацииЛогов.КаталогЛогов;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаТЖПутьКФайлуКонфигурацииЛоговОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	Если Не СтрЗаканчиваетсяНа(ЭтотОбъект.НастройкаТЖПутьКФайлуКонфигурацииЛогов, "logcfg.xml") Тогда
		Возврат;
	КонецЕсли;
	ТекстДок = Новый ТекстовыйДокумент();
	ИскомыйТекст = ТекстФайла(ЭтотОбъект.НастройкаТЖПутьКФайлуКонфигурацииЛогов);
	ТекстДок.УстановитьТекст(ИскомыйТекст);
	ТекстДок.Показать("logcfg.xml");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаТЖКаталогТекущихЛоговНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПустаяСтрока(ЭтотОбъект.НастройкаТЖКаталогЛогов) Тогда
		Возврат;
	КонецЕсли;
	
	СписокКаталогов = СписокКаталоговЛогов(ЭтотОбъект.НастройкаТЖКаталогЛогов);
	Оповещение = Новый ОписаниеОповещения("НастройкаТЖКаталогТекущихЛоговНачалоВыбора_Завершение", ЭтотОбъект, Новый Структура());
	СписокКаталогов.ПоказатьВыборЭлемента(Оповещение, "Выбор каталога", ЭтотОбъект.НастройкаТЖКаталогТекущихЛогов);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаТЖКаталогТекущихЛоговНачалоВыбора_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("ЭлементСпискаЗначений") Тогда
		ЭтотОбъект.НастройкаТЖКаталогТекущихЛогов = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ЗначениеВыбора = Новый Структура(КонсольЗапросов_СтрокаНастроек());
	ЗаполнитьЗначенияСвойств(ЗначениеВыбора, ЭтотОбъект);
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КонсольЗапросов_СтрокаНастроек()
	
	Возврат "НастройкаМаксКоличествоСтрок,НастройкаВыводитьСвойстваВыборки,НастройкаВыделятьОтрицательные,
	|НастройкаФиксированнаяШиринаКолонок,НастройкаШиринаКолонок,НастройкаМаксШиринаКолонок,НастройкаПереноситьТекст,НастройкаФорматЧисла,НастройкаВыполнятьВФоне,НастройкаСохранятьРезультаты,
	|НастройкаРаботаСТЖ,НастройкаТЖПутьКФайлуКонфигурацииЛогов,НастройкаТЖКаталогЛогов,НастройкаТЖКаталогТекущихЛогов,НастройкаТЖТегЗапроса";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Знач Форма)
	
	Форма.Элементы.НастройкаШиринаКолонок.Доступность = Форма.НастройкаФиксированнаяШиринаКолонок;
	Форма.Элементы.НастройкаМаксШиринаКолонок.Доступность = Не Форма.НастройкаФиксированнаяШиринаКолонок;
	Форма.Элементы.НастройкаРаботаСТЖ.Доступность = Не Форма.ПеременныеОкружения.БезопасныйРежим;
	Форма.Элементы.НастройкаТЖПутьКФайлуКонфигурацииЛогов.Доступность = Форма.НастройкаРаботаСТЖ;
	Форма.Элементы.НастройкаТЖКаталогЛогов.Доступность = Форма.НастройкаРаботаСТЖ;
	Форма.Элементы.НастройкаТЖКаталогТекущихЛогов.Доступность = Форма.НастройкаРаботаСТЖ;
	Форма.Элементы.НастройкаТЖТегЗапроса.Доступность = Форма.НастройкаРаботаСТЖ;
	
КонецПроцедуры

&НаСервере
Функция ТекстФайла(Знач ПутьКФайлу)
	
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	Результат = ОбъектОбработки.ТекстФайла(ПутьКФайлу);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция СписокКаталоговЛогов(Знач КаталогЛогов)
	
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	Результат = ОбъектОбработки.РаботаСТЖ_СписокКаталоговЛогов(КаталогЛогов);
	
	Возврат Результат;
	
КонецФункции








